       IDENTIFICATION DIVISION.                                         00010009
       PROGRAM-ID PGMVAC7C.                                             00020009
       AUTHOR.        D. CAZABAT.                                       00030009
       INSTALLATION.  CURSO CODEKI.                                     00040009
       DATE-WRITTEN.  30/04/2025.                                       00050009
       DATE-COMPILED. 30/04/2025.                                       00060009
      **********************************************************        00070009
      *                                                        *        00080009
      *  OBJETIVO: PRACTICA CODIFICACION COBOL                 *        00090009
      *  ASINCRONICA 8: VALIDACION                             *        00100009
      *                                                        *        00110009
      **********************************************************        00120009
       ENVIRONMENT DIVISION.                                            00130009
       INPUT-OUTPUT SECTION.                                            00140009
       FILE-CONTROL.                                                    00150009
             SELECT NOVCLI   ASSIGN DNOVCLI                             00160009
                    FILE STATUS IS FS-NOVCLI.                           00170009
                                                                        00180009
             SELECT S1GRAB   ASSIGN DDGRAB                              00190009
                    FILE STATUS IS FS-S1GRAB.                           00200009
                                                                        00210009
             SELECT S2ERRR   ASSIGN DDERRR                              00220009
                    FILE STATUS IS FS-S2ERRR.                           00230009
                                                                        00240009
       DATA DIVISION.                                                   00250009
       FILE SECTION.                                                    00260009
       FD NOVCLI                                                        00270009
            BLOCK CONTAINS 0 RECORDS                                    00280009
            RECORDING MODE IS F.                                        00290009
                                                                        00300009
       01 REG-NOVCLI    PIC X(50).                                      00310009
                                                                        00320009
       FD S1GRAB                                                        00330009
            BLOCK CONTAINS 0 RECORDS                                    00340009
            RECORDING MODE IS F.                                        00350009
                                                                        00360009
       01 REG-S1GRAB     PIC X(55).                                     00370009
                                                                        00380009
       FD S2ERRR                                                        00390009
            BLOCK CONTAINS 0 RECORDS                                    00400009
            RECORDING MODE IS F.                                        00410009
                                                                        00420009
       01 REG-S2ERRR     PIC X(55).                                     00430009
                                                                        00440009
      **************************************                            00450009
       WORKING-STORAGE SECTION.                                         00460009
      **************************************                            00470009
       77  FILLER        PIC X(26) VALUE '* INICIO WORKING-STORAGE *'.  00480009
                                                                        00490009
      **************************************                            00500009
      * FILE STATUS                                                     00510009
      **************************************                            00520009
                                                                        00530009
       77  FS-NOVCLI      PIC XX    VALUE SPACES.                       00540009
           88  FS-NOVCLI-FIN        VALUE '10'.                         00550009
                                                                        00560009
       77  FS-S1GRAB      PIC XX    VALUE SPACES.                       00570009
           88  FS-S1GRAB-FIN        VALUE '10'.                         00580009
                                                                        00590009
       77  FS-S2ERRR      PIC XX    VALUE SPACES.                       00600009
           88  FS-S2ERRR-FIN        VALUE '10'.                         00610009
                                                                        00620009
      ***************************************************************** 00630009
      * CONTADOR DE TOTALES                                             00640009
      ***************************************************************** 00630009
       77  WS-CANT-LEIDOS          PIC 9(5)      VALUE ZEROS.           00650009
       77  WS-EDIT-LEIDOS          PIC ZZZ9      VALUE ZEROS.           00660009
       77  WS-CANT-GRAB            PIC 9(5)      VALUE ZEROS.           00670009
       77  WS-EDIT-GRAB            PIC ZZZ9      VALUE ZEROS.           00680009
       77  WS-CANT-ERRR            PIC 9(5)      VALUE ZEROS.           00690009
       77  WS-EDIT-ERRR            PIC ZZZ9      VALUE ZEROS.           00700009
       77  WS-LINEA-SEPARACION     PIC X(40) 
           VALUE '----------------------------------------'.

      ***************************************************************** 00630009
      * VARIABLES DE CONTROL DE ERRORES                                 
      ***************************************************************** 00630009
       77  WS-ERROR-FECHA          PIC X(50)     VALUE SPACES.          00710009
       77  WS-ERROR-TPDOC          PIC X(50)     VALUE SPACES.          00720009
       77  WS-ERROR-SUCUR          PIC X(50)     VALUE SPACES.          00730009
       77  WS-ERROR-TPCTA          PIC X(50)     VALUE SPACES.          00740009

      ********     FECHA DE PROCESO     *****************************   00750009
       77 WS-FECHA-AAAAMMDD        PIC X(8).
       77 WS-FECHA-NUMERO          PIC 9(8).                            00760009
       77 WS-FECHA-AAAA            PIC X(4).
       77 WS-FECHA-NUMDESDE        PIC 9(8)      VALUE ZEROS.
       77 WS-FECHA-NUMHASTA        PIC 9(8)      VALUE ZEROS.
       01  WS-FECHA.                                                    00770009
           05 WS-FECHA-AA          PIC 9(2).                            00780009
           05 WS-FECHA-MM          PIC 9(2).                            00790009
           05 WS-FECHA-DD          PIC 9(2).                            00800009
                                                                        00810009
       01  WS-FECHA-VALIDA         PIC X(1) VALUE 'S'.                  00820009
           88 FECHA-VALIDA                  VALUE 'S'.                  00830009
           88 FECHA-NO-VALIDA               VALUE 'N'.                  00840009
                                                                        00850009
       01  WS-DIAS-POR-MES.                                             00860009
           05 WS-DIAS-MES             PIC 9(2) OCCURS 12 TIMES.         00870009
                                                                        00880009
       01  WS-AUX.                                                      00890009
           05 WS-RESTO                PIC 9(4).                         00900009
           05 WS-RESTO-CUATRO         PIC 9(4).                         00910009
           05 WS-RESTO-CIEN           PIC 9(4).                         00920009
           05 WS-RESTO-CUATROCIENTOS  PIC 9(4).                         00930009
      *************  BANDERA DE PROCESAD DE REGISTRO  **************    00940009
       01  WS-PROCESAR-REG      PIC X VALUE 'N'.                        00950009
           88 WS-PROCESAR-SI    VALUE 'S'.                              00960009
           88 WS-PROCESAR-NO    VALUE 'N'.                              00970009
      ***************************************************************.  00980009
       01  FILLER        PIC X(26) VALUE '* FINAL  WORKING-STORAGE *'.  00990009
      ***************************************************************.  01000009
                                                                        01010009
      ***************  FORMATO DE ENTRADA DE DATOS **************       01020009
               COPY CPNOVCLI.                                           01030009
      ***************  FORMATO DE SALIDA DE DATOS ***************       01040009
               COPY CPNCLIV.                                            01050009
                                                                        01060009
       PROCEDURE DIVISION.                                              01070009
      **************************************                            01080009
      *                                    *                            01090009
      *  CUERPO PRINCIPAL DEL PROGRAMA     *                            01100009
      *                                    *                            01110009
      **************************************                            01120009
       MAIN-PROGRAM.                                                    01130009
                                                                        01140009
           PERFORM 1000-INICIO   THRU    1000-INICIO-F                  01150009
                                                                        01160009
           PERFORM 2000-PROCESO  THRU    2000-PROCESO-F                 01170009
                   UNTIL FS-NOVCLI-FIN                                  01180009
                                                                        01190009
           PERFORM 3000-FINAL    THRU    3000-FINAL-F                   01200009
           .                                                            01210009
       F-MAIN-PROGRAM. GOBACK.                                          01220009
                                                                        01230009
      **************************************                            01240009
      *                                    *                            01250009
      *  CUERPO INICIO APERTURA ARCHIVOS   *                            01260009
      *                                    *                            01270009
      **************************************                            01280009
       1000-INICIO.                                                     01290009
           ACCEPT WS-FECHA FROM DATE.                                   01300009
           MOVE '20' TO WS-FECHA-AAAA(1:2)
           MOVE WS-FECHA-AA TO WS-FECHA-AAAA(3:2)
           DISPLAY 'FECHA DE PROCESO: 
              WS-FECHA-DD '/' WS-FECHA-MM '/' WS-FECHA-AAAA
           DISPLAY WS-LINEA-SEPARACION 
                                                                        
           OPEN INPUT  NOVCLI                                           
                                                                        
           IF FS-NOVCLI IS NOT EQUAL '00'                               
              DISPLAY '* ERROR EN OPEN SUCURSAL= '  FS-NOVCLI           
              MOVE 9999 TO RETURN-CODE                                  
              SET  FS-NOVCLI-FIN TO TRUE                                
           ELSE                                                         
              PERFORM 2100-LEER                                         
                 THRU 2100-LEER-F                                       
           END-IF                                                       
                                                                        
           OPEN OUTPUT S1GRAB                                           
           IF FS-S1GRAB IS NOT EQUAL '00'                               
              DISPLAY '* ERROR EN OPEN SALIDA = '  FS-S1GRAB            
              MOVE 9999 TO RETURN-CODE                                  
              SET  FS-NOVCLI-FIN TO TRUE                                
           END-IF                                                       
      *                                                                 
           OPEN OUTPUT S2ERRR                                           
           IF FS-S2ERRR IS NOT EQUAL '00'                               
              DISPLAY '* ERROR EN OPEN SALIDA = '  FS-S2ERRR            
              MOVE 9999 TO RETURN-CODE                                  
              SET  FS-NOVCLI-FIN TO TRUE                                
           END-IF                                                       
           .                                                            
       1000-INICIO-F. EXIT.                                             
                                                                        
      **************************************                            
      *                                    *                            
      *  CUERPO PRINCIPAL DE PROCESOS      *                            
      *                                    *                            
      **************************************                            
       2000-PROCESO.                                                    
      *                                                                 
           IF WS-PROCESAR-SI                                            
              ADD 1 TO WS-CANT-GRAB                                     
              PERFORM 2200-GRABAR-GRAB                                  
                 THRU 2200-GRABAR-GRAB-F                                
           ELSE                                                         
             ADD 1 TO WS-CANT-ERRR                                      
             PERFORM 2300-GRABAR-ERRR                                   
                THRU 2300-GRABAR-ERRR-F                                 
           END-IF                                                       
                                                                        
           PERFORM 2100-LEER                                            
              THRU 2100-LEER-F                                          
           .                                                            
       2000-PROCESO-F. EXIT.                                            
                                                                        
      **************************************                            
      *  LEER REGISTROS                    *                            
      **************************************                            
       2100-LEER.                                                       
           READ NOVCLI INTO WS-REG-NOVCLIE                              
           MOVE SPACES TO WS-ERROR-FECHA                                
           MOVE SPACES TO WS-ERROR-TPDOC                                
           MOVE SPACES TO WS-ERROR-SUCUR                                
           MOVE SPACES TO WS-ERROR-TPCTA                                
                                                                        
           EVALUATE FS-NOVCLI                                           
             WHEN '00'                                                  
               ADD 1 TO WS-CANT-LEIDOS                                  
               MOVE 'S' TO WS-PROCESAR-REG                              
                                                                        
               *> Asigno el campo de fecha de la copylib a la           
               *> variable de trabajo WS-FECHA-AAAAMMDD                 
               MOVE NOV-CLI-FECHA(1:8) TO WS-FECHA-AAAAMMDD             
               PERFORM 4000-VALIDAR-FECHA                               
                  THRU 4000-VALIDAR-FECHA-F                             
               IF NOT FECHA-VALIDA                                      
                  MOVE 'N' TO WS-PROCESAR-REG                           
                  MOVE 'LA FECHA INVALIDA ' TO WS-ERROR-FECHA           
               END-IF                                                   
                                                                        
               *> Se verifica si el tipo de documento es correcto       
               IF NOT (NOV-TIP-DOC = 'DU' OR                            
                  NOV-TIP-DOC = 'PA' OR                                 
                  NOV-TIP-DOC = 'PE' OR                                 
                  NOV-TIP-DOC = 'CI')                                   
                  MOVE 'N' TO WS-PROCESAR-REG                           
                  MOVE 'TIPO DE DOCUMENTO INCORRECTO' TO WS-ERROR-TPDOC 
               END-IF                                                   
                                                                        
                *> Se verifica si la sucursal es correcta               
               IF NOT (NOV-SUC >= 1 AND NOV-SUC <= 99)                  
                  MOVE 'N' TO WS-PROCESAR-REG                           
                  MOVE 'SUCURSAL INCORRECTA' TO WS-ERROR-SUCUR          
               END-IF                                                   
                                                                        
                *> Se verifica el tipo de cuenta es correcto            
               IF NOT (NOV-CLI-TIPO = 1 OR                              
                  NOV-CLI-TIPO = 2 OR                                   
                  NOV-CLI-TIPO = 3)                                     
                  MOVE 'N' TO WS-PROCESAR-REG                           
                  MOVE 'TIPO DE CUENTA INCORRECTA' TO WS-ERROR-TPCTA    
               END-IF                                                   
                                                                        
             WHEN '10'                                                  
               CONTINUE                                                 
                                                                        
             WHEN OTHER                                                 
               DISPLAY '* ERROR EN LECTURA SUCURSAL= ' FS-NOVCLI        
               MOVE 9999 TO RETURN-CODE                                 
               SET FS-NOVCLI-FIN  TO TRUE                               
           END-EVALUATE                                                 
           .                                                            
       2100-LEER-F. EXIT.                                               
                                                                        
      **************************************                            
      *  GRABAR LOS REGISTROS OK           *                            
      **************************************                            
       2200-GRABAR-GRAB.                                                
           MOVE WS-CANT-GRAB TO NOV-SECUEN                              
           MOVE WS-REG-NOVCLIE TO NOV-RESTO                             
           WRITE REG-S1GRAB FROM REG-NOVCLIE-VAL                        
                                                                        
           IF FS-S1GRAB IS NOT EQUAL '00'                               
              DISPLAY '* ERROR EN GRABAR REGISTRO= ' FS-S1GRAB          
              MOVE 9999 TO RETURN-CODE                                  
              SET FS-NOVCLI-FIN  TO TRUE                                
           END-IF.                                                      
       2200-GRABAR-GRAB-F. EXIT.                                        
      **************************************                            
      *  GRABAR ERRRULINO                  *                            
      **************************************                            
       2300-GRABAR-ERRR.                                                
           MOVE WS-CANT-ERRR TO NOV-SECUEN                              
           MOVE WS-REG-NOVCLIE TO NOV-RESTO                             
           *> Display de los registros con error                        
           DISPLAY " TIPO DOC " NOV-TIP-DOC " NRO DOC " NOV-NRO-DOC     
           DISPLAY "   CONTINENE ESTOS ERRORES: "                       
           IF WS-ERROR-FECHA NOT = SPACES                               
              DISPLAY "    - " WS-ERROR-FECHA                           
           END-IF                                                       
           IF WS-ERROR-TPDOC NOT = SPACES                               
              DISPLAY "    - " WS-ERROR-TPDOC                           
           END-IF                                                       
           IF WS-ERROR-SUCUR NOT = SPACES                               
              DISPLAY "    - " WS-ERROR-SUCUR                           
           END-IF                                                       
           IF WS-ERROR-TPCTA NOT = SPACES                               
              DISPLAY "    - " WS-ERROR-TPCTA                           
           END-IF                                                       
           DISPLAY WS-LINEA-SEPARACION 
           WRITE REG-S2ERRR FROM REG-NOVCLIE-VAL                        
                                                                        
           IF FS-S2ERRR IS NOT EQUAL '00'                               
              DISPLAY '* ERROR EN GRABAR REGISTRO= ' FS-S2ERRR          
              MOVE 9999 TO RETURN-CODE                                  
              SET FS-NOVCLI-FIN  TO TRUE                                
           END-IF.                                                      
       2300-GRABAR-ERRR-F. EXIT.                                        
                                                                        
                                                                        
      **************************************                            
      *                                    *                            
      *  CUERPO FINAL                      *                            
      *                                    *                            
      **************************************                            
       3000-FINAL.                                                      
           IF RETURN-CODE NOT EQUAL 9999                                
             PERFORM 3010-CLOSE-FILES                                   
                THRU 3010-CLOSE-FILES-F                                 
             PERFORM 3020-MOSTRAR-TOTALES                               
                THRU 3020-MOSTRAR-TOTALES-F                             
           END-IF.                                                      
       3000-FINAL-F. EXIT.                                              
      **************************************                            
      *   CERRAR ARCHIVOS                  *                            
      **************************************                            
       3010-CLOSE-FILES.                                                
           CLOSE NOVCLI                                                 
              IF FS-NOVCLI  IS NOT EQUAL '00'                           
                DISPLAY '* ERROR EN CLOSE SUCURSAL= ' FS-NOVCLI         
                MOVE 9999 TO RETURN-CODE                                
             END-IF                                                     
                                                                        
           CLOSE S1GRAB                                                 
              IF FS-S1GRAB   IS NOT EQUAL '00'                          
                DISPLAY '* ERROR EN CLOSE GRABNINO= ' FS-S1GRAB         
                MOVE 9999 TO RETURN-CODE                                
             END-IF                                                     
                                                                        
           CLOSE S2ERRR                                                 
              IF FS-S2ERRR   IS NOT EQUAL '00'                          
                DISPLAY '* ERROR EN CLOSE GRABNINO= ' FS-S2ERRR         
                MOVE 9999 TO RETURN-CODE                                
             END-IF                                                     
           .                                                            
       3010-CLOSE-FILES-F. EXIT.                                        
                                                                        
      **************************************                            
      *  MOSTRAR TOTALES                   *                            
      **************************************                            
       3020-MOSTRAR-TOTALES.                                            
           MOVE WS-CANT-LEIDOS TO WS-EDIT-LEIDOS                        
           MOVE WS-CANT-GRAB   TO WS-EDIT-GRAB                          
           MOVE WS-CANT-ERRR   TO WS-EDIT-ERRR                          
           DISPLAY WS-LINEA-SEPARACION 
           DISPLAY ' TOTAL DE ENTRADAS LEIDAS '  WS-EDIT-LEIDOS         
           DISPLAY ' TOTAL DE GRABADOS OK     '  WS-EDIT-GRAB           
           DISPLAY ' TOTAL DE GRABADOS NO OK  '  WS-EDIT-ERRR           
           DISPLAY WS-LINEA-SEPARACION.
       3020-MOSTRAR-TOTALES-F. EXIT.                                    
                                                                        
      *----------------------------------------------------------------*
      * VALIDAR FECHA EN FORMATO AAAAMMDD                              *
      *----------------------------------------------------------------*
       4000-VALIDAR-FECHA.
           *> Se asigna la fecha de proceso a la variable de trabajo
           MOVE WS-FECHA-AAAAMMDD TO WS-FECHA-NUMERO

           *> Se asigna la fecha de proceso a FECHA DESDE
           MOVE FUNCTION CURRENT-DATE(1:4) TO WS-FECHA-NUMDESDE(1:4)
           MOVE '0101' TO WS-FECHA-NUMDESDE(5:4)

           *> Se asigna la fecha de proceso a FECHA HASTA
           MOVE FUNCTION CURRENT-DATE(1:8) TO WS-FECHA-NUMHASTA
           IF FUNCTION TEST-DATE-YYYYMMDD(WS-FECHA-NUMERO) = 0
             IF WS-FECHA-NUMDESDE <= WS-FECHA-NUMERO AND
                WS-FECHA-NUMERO <= WS-FECHA-NUMHASTA
                MOVE 'S' TO WS-FECHA-VALIDA
             ELSE
                MOVE 'N' TO WS-FECHA-VALIDA
             END-IF
           ELSE                                                         
              MOVE 'N' TO WS-FECHA-VALIDA                               
           END-IF                                                       
           .                                                            
       4000-VALIDAR-FECHA-F. EXIT.                                      

