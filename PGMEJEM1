       IDENTIFICATION DIVISION.                                         
      *                                                        *        
        PROGRAM-ID PGMEJEM1.                                            
      **********************************************************        
      *                                                        *        
      *  OBJETIVO: PRACTICA CODIFICACION COBOL                 *        
      *                                                        *        
      *                                                        *        
      **********************************************************        
       ENVIRONMENT DIVISION.                                            
       INPUT-OUTPUT SECTION.                                            
       FILE-CONTROL.                                                    
             SELECT E1SUCUR   ASSIGN DDSUCUR                            
                    FILE STATUS IS FS-E1SUCUR.                          
                                                                        
             SELECT S1FEME    ASSIGN DDFEME                             
                    FILE STATUS IS FS-S1FEME.                           
                                                                        
             SELECT S2MASC    ASSIGN DDMASC                             
                    FILE STATUS IS FS-S2MASC.                           
                                                                        
       DATA DIVISION.                                                   
       FILE SECTION.                                                    
       FD E1SUCUR                                                       
            BLOCK CONTAINS 0 RECORDS                                    
            RECORDING MODE IS F.                                        
                                                                        
       01 REG-E1SUCUR    PIC X(93).                                     
                                                                        
       FD S1FEME                                                        
            BLOCK CONTAINS 0 RECORDS                                    
            RECORDING MODE IS F.                                        
                                                                        
       01 REG-S1FEME     PIC X(93).                                     
                                                                        
       FD S2MASC                                                        
            BLOCK CONTAINS 0 RECORDS                                    
            RECORDING MODE IS F.                                        
                                                                        
       01 REG-S2MASC     PIC X(93).                                     
                                                                        
      **************************************                            
       WORKING-STORAGE SECTION.                                         
      **************************************                            
       77  FILLER        PIC X(26) VALUE '* INICIO WORKING-STORAGE *'.  
                                                                        
      **************************************                            
      * FILE STATUS                                                     
      **************************************                            
                                                                        
       77  FS-E1SUCUR      PIC XX    VALUE SPACES.                      
           88  FS-E1SUCUR-FIN        VALUE '10'.                        
                                                                        
       77  FS-S1FEME       PIC XX    VALUE SPACES.                      
           88  FS-S1FEME-FIN         VALUE '10'.                        
                                                                        
       77  FS-S2MASC       PIC XX    VALUE SPACES.                      
           88  FS-S2MASC-FIN         VALUE '10'.                        
                                                                        
      **************************************                            
      * CONTADOR DE TOTALES                                             
       77  WS-CANT-LEIDOS          PIC 9(3)      VALUE ZEROS.           
       77  WS-CANT-FEME            PIC 9(3)      VALUE ZEROS.           
       77  WS-CANT-MASC            PIC 9(3)      VALUE ZEROS.           
                                                                        
      * CONTADOR DE REGISTROS QUE CUMPLEN LA CONDICION                  
       77  WS-CANT-CONDICION       PIC 9(3)      VALUE ZEROS.           
       77  WS-COND-EDIT            PIC Z(3)      VALUE ZEROS.           
                                                                        
      **************************************                            
      * COPY PARA ESTRUCTURA DE DATOS                                   
      **************************************                            
                                                                        
       COPY CPSUCUR.                                                    
                                                                        
       01  FILLER        PIC X(26) VALUE '* FINAL  WORKING-STORAGE *'.  
                                                                        
      ***************************************************************.  
       PROCEDURE DIVISION.                                              
      **************************************                            
      *                                    *                            
      *  CUERPO PRINCIPAL DEL PROGRAMA     *                            
      *                                    *                            
      **************************************                            
       MAIN-PROGRAM.                                                    
                                                                        
           PERFORM 1000-INICIO   THRU    1000-INICIO-F                  
                                                                        
           PERFORM 2000-PROCESO  THRU    2000-PROCESO-F                 
                   UNTIL FS-E1SUCUR-FIN                                 
                                                                        
           PERFORM 3000-FINAL    THRU    3000-FINAL-F                   
           .                                                            
       F-MAIN-PROGRAM. GOBACK.                                          
                                                                        
      **************************************                            
      *                                    *                            
      *  CUERPO INICIO APERTURA ARCHIVOS   *                            
      *                                    *                            
      **************************************                            
       1000-INICIO.                                                     
                                                                        
      *                                                                 
           OPEN INPUT  E1SUCUR                                          
                                                                        
           IF FS-E1SUCUR IS NOT EQUAL '00'                              
              DISPLAY '* ERROR EN OPEN SUCURSAL= '  FS-E1SUCUR          
              MOVE 9999 TO RETURN-CODE                                  
              SET  FS-E1SUCUR-FIN TO TRUE                               
           ELSE                                                         
              PERFORM 2100-LEER          THRU 2100-LEER-F               
           END-IF                                                       
      *                                                                 
           OPEN OUTPUT S1FEME                                           
                                                                        
           IF FS-S1FEME IS NOT EQUAL '00'                               
              DISPLAY '* ERROR EN OPEN SALIDA = '  FS-S1FEME            
              MOVE 9999 TO RETURN-CODE                                  
              SET  FS-E1SUCUR-FIN TO TRUE                               
           END-IF                                                       
      *                                                                 
           OPEN OUTPUT S2MASC                                           
                                                                        
           IF FS-S2MASC IS NOT EQUAL '00'                               
              DISPLAY '* ERROR EN OPEN SALIDA = '  FS-S2MASC            
              MOVE 9999 TO RETURN-CODE                                  
              SET  FS-E1SUCUR-FIN TO TRUE                               
           END-IF                                                       
           .                                                            
       1000-INICIO-F. EXIT.                                             
                                                                        
      **************************************                            
      *                                    *                            
      *  CUERPO PRINCIPAL DE PROCESOS      *                            
      *                                    *                            
      **************************************                            
       2000-PROCESO.                                                    
      *                                                                 
           IF WS-SUC-SEXO  EQUAL 'F'                                    
              PERFORM 2200-GRABAR-FEME THRU 2200-GRABAR-FEME-F          
           ELSE                                                         
              PERFORM 2300-GRABAR-MASC THRU 2300-GRABAR-MASC-F          
           END-IF                                                       
                                                                        
           PERFORM 2100-LEER           THRU 2100-LEER-F                 
           .                                                            
       2000-PROCESO-F. EXIT.                                            
                                                                        
      **************************************                            
      *  LEER REGISTROS                    *                            
      **************************************                            
       2100-LEER.                                                       
           READ E1SUCUR           INTO WS-REG-SUCURSAL                  
                                                                        
           EVALUATE FS-E1SUCUR                                          
             WHEN '00'                                                  
                            ADD 1 TO WS-CANT-LEIDOS                     
              WHEN '10'                                                 
                CONTINUE                                                
                                                                        
              WHEN OTHER                                                
              DISPLAY '* ERROR EN LECTURA SUCURSAL= ' FS-E1SUCUR        
              MOVE 9999 TO RETURN-CODE                                  
              SET FS-E1SUCUR-FIN  TO TRUE                               
                                                                        
           END-EVALUATE                                                 
           .                                                            
       2100-LEER-F. EXIT.                                               
                                                                        
      **************************************                            
      *  GRABAR FEMENINO                   *                            
      **************************************                            
       2200-GRABAR-FEME.                                                
           WRITE REG-S1FEME   FROM WS-REG-SUCURSAL                      
                                                                        
           EVALUATE FS-S1FEME                                           
             WHEN '00'                                                  
                            ADD 1 TO WS-CANT-FEME                       
              WHEN '10'                                                 
                CONTINUE                                                
                                                                        
           WHEN OTHER                                                   
              DISPLAY '* ERROR EN GRABAR FEMENINO = ' FS-S1FEME         
              MOVE 9999 TO RETURN-CODE                                  
              SET FS-E1SUCUR-FIN  TO TRUE                               
                                                                        
           END-EVALUATE                                                 
           .                                                            
       2200-GRABAR-FEME-F. EXIT.                                        
      **************************************                            
      *  GRABAR MASCULINO                  *                            
      **************************************                            
       2300-GRABAR-MASC.                                                
           WRITE REG-S2MASC   FROM WS-REG-SUCURSAL                      
                                                                        
           EVALUATE FS-S2MASC                                           
             WHEN '00'                                                  
                            ADD 1 TO WS-CANT-MASC                       
              WHEN '10'                                                 
                CONTINUE                                                
                                                                        
           WHEN OTHER                                                   
              DISPLAY '* ERROR EN GRABAR MASCULINO= ' FS-S2MASC         
              MOVE 9999 TO RETURN-CODE                                  
              SET FS-E1SUCUR-FIN  TO TRUE                               
                                                                        
           END-EVALUATE                                                 
           .                                                            
       2300-GRABAR-MASC-F. EXIT.                                        
                                                                        
                                                                        
      **************************************                            
      *                                    *                            
      *  CUERPO FINAL                      *                            
      *                                    *                            
      **************************************                            
       3000-FINAL.                                                      
           IF RETURN-CODE NOT EQUAL 9999                                
            PERFORM  3010-CLOSE-FILES      THRU  3010-CLOSE-FILES-F     
            PERFORM  3020-MOSTRAR-TOTALES  THRU  3020-MOSTRAR-TOTALES-F 
           END-IF                                                       
           .                                                            
       3000-FINAL-F. EXIT.                                              
      **************************************                            
      *   CERRAR ARCHIVOS                  *                            
      **************************************                            
       3010-CLOSE-FILES.                                                
           CLOSE E1SUCUR                                                
              IF FS-E1SUCUR  IS NOT EQUAL '00'                          
                DISPLAY '* ERROR EN CLOSE SUCURSAL= '                   
                                            FS-E1SUCUR                  
                MOVE 9999 TO RETURN-CODE                                
             END-IF                                                     
                                                                        
           CLOSE S1FEME                                                 
              IF FS-S1FEME   IS NOT EQUAL '00'                          
                DISPLAY '* ERROR EN CLOSE FEMENINO= '                   
                                            FS-S1FEME                   
                MOVE 9999 TO RETURN-CODE                                
             END-IF                                                     
                                                                        
           CLOSE S2MASC                                                 
              IF FS-S2MASC   IS NOT EQUAL '00'                          
                DISPLAY '* ERROR EN CLOSE FEMENINO= '                   
                                            FS-S2MASC                   
                MOVE 9999 TO RETURN-CODE                                
             END-IF                                                     
           .                                                            
       3010-CLOSE-FILES-F. EXIT.                                        
                                                                        
      **************************************                            
      *  MOSTRAR TOTALES                   *                            
      **************************************                            
       3020-MOSTRAR-TOTALES.                                            
           DISPLAY ' TOTAL DE ENTRADAS LEIDAS '  WS-CANT-LEIDOS         
           DISPLAY ' TOTAL DE FEMENINOS       '  WS-CANT-FEME           
           DISPLAY ' TOTAL DE MASCULINOS      '  WS-CANT-MASC           
           .                                                            
       3020-MOSTRAR-TOTALES-F. EXIT.                                    
                                                                        
