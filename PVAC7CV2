       IDENTIFICATION DIVISION.                                         
       PROGRAM-ID PGMVAC7C.                                             
       AUTHOR.        D. CAZABAT.                                       
       INSTALLATION.  CURSO CODEKI.                                     
       DATE-WRITTEN.  30/04/2025.                                       
       DATE-COMPILED. 30/04/2025.                                       
      **********************************************************        
      *                                                        *        
      *  OBJETIVO: PRACTICA CODIFICACION COBOL                 *        
      *  ASINCRONICA 8: VALIDACION                             *        
      *                                                        *        
      **********************************************************        
       ENVIRONMENT DIVISION.                                            
       INPUT-OUTPUT SECTION.                                            
       FILE-CONTROL.                                                    
             SELECT NOVCLI   ASSIGN DNOVCLI                             
                    FILE STATUS IS FS-NOVCLI.                           
                                                                        
             SELECT S1GRAB   ASSIGN DDGRAB                              
                    FILE STATUS IS FS-S1GRAB.                           
                                                                        
             SELECT S2ERRR   ASSIGN DDERRR                              
                    FILE STATUS IS FS-S2ERRR.                           
                                                                        
       DATA DIVISION.                                                   
       FILE SECTION.                                                    
       FD NOVCLI                                                        
            BLOCK CONTAINS 0 RECORDS                                    
            RECORDING MODE IS F.                                        
                                                                        
       01 REG-NOVCLI    PIC X(50).                                      
                                                                        
       FD S1GRAB                                                        
            BLOCK CONTAINS 0 RECORDS                                    
            RECORDING MODE IS F.                                        
                                                                        
       01 REG-S1GRAB     PIC X(55).                                     
                                                                        
       FD S2ERRR                                                        
            BLOCK CONTAINS 0 RECORDS                                    
            RECORDING MODE IS F.                                        
                                                                        
       01 REG-S2ERRR     PIC X(55).                                     
                                                                        
      **************************************                            
       WORKING-STORAGE SECTION.                                         
      **************************************                            
       77  FILLER        PIC X(26) VALUE '* INICIO WORKING-STORAGE *'.  
                                                                        
      **************************************                            
      * FILE STATUS                                                     
      **************************************                            
                                                                        
       77  FS-NOVCLI      PIC XX    VALUE SPACES.                       
           88  FS-NOVCLI-FIN        VALUE '10'.                         
                                                                        
       77  FS-S1GRAB      PIC XX    VALUE SPACES.                       
           88  FS-S1GRAB-FIN        VALUE '10'.                         
                                                                        
       77  FS-S2ERRR      PIC XX    VALUE SPACES.                       
           88  FS-S2ERRR-FIN        VALUE '10'.                         
                                                                        
      ***************************************************************** 
      * CONTADOR DE TOTALES                                             
      ***************************************************************** 
       77  WS-CANT-LEIDOS          PIC 9(5)      VALUE ZEROS.           
       77  WS-EDIT-LEIDOS          PIC ZZZ9      VALUE ZEROS.           
       77  WS-CANT-GRAB            PIC 9(5)      VALUE ZEROS.           
       77  WS-EDIT-GRAB            PIC ZZZ9      VALUE ZEROS.           
       77  WS-CANT-ERRR            PIC 9(5)      VALUE ZEROS.           
       77  WS-EDIT-ERRR            PIC ZZZ9      VALUE ZEROS.           
       77  WS-LINEA-SEPARACION     PIC X(40)     VALUE ALL '-'.
                                                                        
      ***************************************************************** 
      * VARIABLES DE CONTROL DE ERRORES                                 
      ***************************************************************** 
       77  WS-ERROR-FECHA          PIC X(50)     VALUE SPACES.          
       77  WS-ERROR-TPDOC          PIC X(50)     VALUE SPACES.          
       77  WS-ERROR-SUCUR          PIC X(50)     VALUE SPACES.          
       77  WS-ERROR-TPCTA          PIC X(50)     VALUE SPACES.          
                                                                        
      ********     FECHA DE PROCESO     *****************************   
       77 WS-FECHA-AAAAMMDD        PIC X(8).                            
       01  WS-FECHA.                                                    
           05 WS-FECHA-AA          PIC 9(2).                            
           05 WS-FECHA-MM          PIC 9(2).                            
           05 WS-FECHA-DD          PIC 9(2).                            
                                                                        
       01  WS-FECHA-VALIDA         PIC X(1) VALUE 'S'.                  
           88 FECHA-VALIDA                  VALUE 'S'.                  
           88 FECHA-NO-VALIDA               VALUE 'N'.                  
                                                                        
       01  WS-DIAS-POR-MES.                                             
           05 WS-DIAS-MES             PIC 9(2) OCCURS 12 TIMES.         
                                                                        
       01  WS-AUX.                                                      
           05 WS-RESTO                PIC 9(4).                         
           05 WS-RESTO-CUATRO         PIC 9(4).                         
           05 WS-RESTO-CIEN           PIC 9(4).                         
           05 WS-RESTO-CUATROCIENTOS  PIC 9(4).                         
      *************  BANDERA DE PROCESAD DE REGISTRO  **************    
       01  WS-PROCESAR-REG      PIC X VALUE 'N'.                        
           88 WS-PROCESAR-SI    VALUE 'S'.                              
           88 WS-PROCESAR-NO    VALUE 'N'.                              
      ***************************************************************.  
       01  FILLER        PIC X(26) VALUE '* FINAL  WORKING-STORAGE *'.  
      ***************************************************************.  
                                                                        
      ***************  FORMATO DE ENTRADA DE DATOS **************       
               COPY CPNOVCLI.                                           
      ***************  FORMATO DE SALIDA DE DATOS ***************       
               COPY CPNCLIV.                                            
                                                                        
       PROCEDURE DIVISION.                                              
      **************************************                            
      *                                    *                            
      *  CUERPO PRINCIPAL DEL PROGRAMA     *                            
      *                                    *                            
      **************************************                            
       MAIN-PROGRAM.                                                    
                                                                        
           PERFORM 1000-INICIO   THRU    1000-INICIO-F                  
                                                                        
           PERFORM 2000-PROCESO  THRU    2000-PROCESO-F                 
                   UNTIL FS-NOVCLI-FIN                                  
                                                                        
           PERFORM 3000-FINAL    THRU    3000-FINAL-F                   
           .                                                            
       F-MAIN-PROGRAM. GOBACK.                                          
                                                                        
      **************************************                            
      *                                    *                            
      *  CUERPO INICIO APERTURA ARCHIVOS   *                            
      *                                    *                            
      **************************************                            
       1000-INICIO.                                                     
           ACCEPT WS-FECHA FROM DATE.                                   
           DISPLAY 'FECHA DE PROCESO: '                                 
              WS-FECHA-DD '/' WS-FECHA-MM '/' WS-FECHA-AA               
           DISPLAY WS-LINEA-SEPARACION                                  
                                                                        
           INITIALIZE WS-DIAS-POR-MES                                   
           MOVE 31 TO WS-DIAS-MES(1)                                    
           MOVE 28 TO WS-DIAS-MES(2)                                    
           MOVE 31 TO WS-DIAS-MES(3)                                    
           MOVE 30 TO WS-DIAS-MES(4)                                    
           MOVE 31 TO WS-DIAS-MES(5)                                    
           MOVE 30 TO WS-DIAS-MES(6)                                    
           MOVE 31 TO WS-DIAS-MES(7)                                    
           MOVE 31 TO WS-DIAS-MES(8)                                    
           MOVE 30 TO WS-DIAS-MES(9)                                    
           MOVE 31 TO WS-DIAS-MES(10)                                   
           MOVE 30 TO WS-DIAS-MES(11)                                   
           MOVE 31 TO WS-DIAS-MES(12)                                   
                                                                        
                                                                        
           OPEN INPUT  NOVCLI                                           
                                                                        
           IF FS-NOVCLI IS NOT EQUAL '00'                               
              DISPLAY '* ERROR EN OPEN SUCURSAL= '  FS-NOVCLI           
              MOVE 9999 TO RETURN-CODE                                  
              SET  FS-NOVCLI-FIN TO TRUE                                
           ELSE                                                         
              PERFORM 2100-LEER                                         
                 THRU 2100-LEER-F                                       
           END-IF                                                       
                                                                        
           OPEN OUTPUT S1GRAB                                           
           IF FS-S1GRAB IS NOT EQUAL '00'                               
              DISPLAY '* ERROR EN OPEN SALIDA = '  FS-S1GRAB            
              MOVE 9999 TO RETURN-CODE                                  
              SET  FS-NOVCLI-FIN TO TRUE                                
           END-IF                                                       
      *                                                                 
           OPEN OUTPUT S2ERRR                                           
           IF FS-S2ERRR IS NOT EQUAL '00'                               
              DISPLAY '* ERROR EN OPEN SALIDA = '  FS-S2ERRR            
              MOVE 9999 TO RETURN-CODE                                  
              SET  FS-NOVCLI-FIN TO TRUE                                
           END-IF                                                       
           .                                                            
       1000-INICIO-F. EXIT.                                             
                                                                        
      **************************************                            
      *                                    *                            
      *  CUERPO PRINCIPAL DE PROCESOS      *                            
      *                                    *                            
      **************************************                            
       2000-PROCESO.                                                    
      *                                                                 
           IF WS-PROCESAR-SI                                            
              ADD 1 TO WS-CANT-GRAB                                     
              PERFORM 2200-GRABAR-GRAB                                  
                 THRU 2200-GRABAR-GRAB-F                                
           ELSE                                                         
             ADD 1 TO WS-CANT-ERRR                                      
             PERFORM 2300-GRABAR-ERRR                                   
                THRU 2300-GRABAR-ERRR-F                                 
           END-IF                                                       
                                                                        
           PERFORM 2100-LEER                                            
              THRU 2100-LEER-F                                          
           .                                                            
       2000-PROCESO-F. EXIT.                                            
                                                                        
      **************************************                            
      *  LEER REGISTROS                    *                            
      **************************************                            
       2100-LEER.                                                       
           READ NOVCLI INTO WS-REG-NOVCLIE                              
           MOVE SPACES TO WS-ERROR-FECHA                                
           MOVE SPACES TO WS-ERROR-TPDOC                                
           MOVE SPACES TO WS-ERROR-SUCUR                                
           MOVE SPACES TO WS-ERROR-TPCTA                                
                                                                        
           EVALUATE FS-NOVCLI                                           
             WHEN '00'                                                  
               ADD 1 TO WS-CANT-LEIDOS                                  
               MOVE 'S' TO WS-PROCESAR-REG                              
                                                                        
               *> Asigno el campo de fecha de la copylib a la           
               *> variable de trabajo WS-FECHA-AAAAMMDD                 
               MOVE NOV-CLI-FECHA(1:8) TO WS-FECHA-AAAAMMDD             
               PERFORM 4000-VALIDAR-FECHA                               
                  THRU 4000-VALIDAR-FECHA-F                             
               IF NOT FECHA-VALIDA                                      
                  MOVE 'N' TO WS-PROCESAR-REG                           
                  MOVE 'LA FECHA INVALIDA ' TO WS-ERROR-FECHA           
               END-IF                                                   
                                                                        
               *> Se verifica si el tipo de documento es correcto       
               IF NOT (NOV-TIP-DOC = 'DU' OR                            
                  NOV-TIP-DOC = 'PA' OR                                 
                  NOV-TIP-DOC = 'PE' OR                                 
                  NOV-TIP-DOC = 'CI')                                   
                  MOVE 'N' TO WS-PROCESAR-REG                           
                  MOVE 'TIPO DE DOCUMENTO INCORRECTO' TO WS-ERROR-TPDOC 
               END-IF                                                   
                                                                        
                *> Se verifica si la sucursal es correcta               
               IF NOT (NOV-SUC >= 1 AND NOV-SUC <= 99)                  
                  MOVE 'N' TO WS-PROCESAR-REG                           
                  MOVE 'SUCURSAL INCORRECTA' TO WS-ERROR-SUCUR          
               END-IF                                                   
                                                                        
                *> Se verifica el tipo de cuenta es correcto            
               IF NOT (NOV-CLI-TIPO = 1 OR                              
                  NOV-CLI-TIPO = 2 OR                                   
                  NOV-CLI-TIPO = 3)                                     
                  MOVE 'N' TO WS-PROCESAR-REG                           
                  MOVE 'TIPO DE CUENTA INCORRECTA' TO WS-ERROR-TPCTA    
               END-IF                                                   
                                                                        
             WHEN '10'                                                  
               CONTINUE                                                 
                                                                        
             WHEN OTHER                                                 
               DISPLAY '* ERROR EN LECTURA SUCURSAL= ' FS-NOVCLI        
               MOVE 9999 TO RETURN-CODE                                 
               SET FS-NOVCLI-FIN  TO TRUE                               
           END-EVALUATE                                                 
           .                                                            
       2100-LEER-F. EXIT.                                               
                                                                        
      **************************************                            
      *  GRABAR LOS REGISTROS OK           *                            
      **************************************                            
       2200-GRABAR-GRAB.                                                
           MOVE WS-CANT-GRAB TO NOV-SECUEN                              
           MOVE WS-REG-NOVCLIE TO NOV-RESTO                             
           WRITE REG-S1GRAB FROM REG-NOVCLIE-VAL                        
                                                                        
           IF FS-S1GRAB IS NOT EQUAL '00'                               
              DISPLAY '* ERROR EN GRABAR REGISTRO= ' FS-S1GRAB          
              MOVE 9999 TO RETURN-CODE                                  
              SET FS-NOVCLI-FIN  TO TRUE                                
           END-IF.                                                      
       2200-GRABAR-GRAB-F. EXIT.                                        
      **************************************                            
      *  GRABAR ERRRULINO                  *                            
      **************************************                            
       2300-GRABAR-ERRR.                                                
           MOVE WS-CANT-ERRR TO NOV-SECUEN                              
           MOVE WS-REG-NOVCLIE TO NOV-RESTO                             
           *> Display de los registros con error                        
           DISPLAY " TIPO DOC " NOV-TIP-DOC " NRO DOC " NOV-NRO-DOC     
           DISPLAY "   CONTINENE ESTOS ERRORES: "                       
           IF WS-ERROR-FECHA NOT = SPACES                               
              DISPLAY "    - " WS-ERROR-FECHA                           
           END-IF                                                       
           IF WS-ERROR-TPDOC NOT = SPACES                               
              DISPLAY "    - " WS-ERROR-TPDOC                           
           END-IF                                                       
           IF WS-ERROR-SUCUR NOT = SPACES                               
              DISPLAY "    - " WS-ERROR-SUCUR                           
           END-IF                                                       
           IF WS-ERROR-TPCTA NOT = SPACES                               
              DISPLAY "    - " WS-ERROR-TPCTA                           
           END-IF                                                       
           DISPLAY WS-LINEA-SEPARACION                                  
           WRITE REG-S2ERRR FROM REG-NOVCLIE-VAL                        
                                                                        
           IF FS-S2ERRR IS NOT EQUAL '00'                               
              DISPLAY '* ERROR EN GRABAR REGISTRO= ' FS-S2ERRR          
              MOVE 9999 TO RETURN-CODE                                  
              SET FS-NOVCLI-FIN  TO TRUE                                
           END-IF.                                                      
       2300-GRABAR-ERRR-F. EXIT.                                        
                                                                        
                                                                        
      **************************************                            
      *                                    *                            
      *  CUERPO FINAL                      *                            
      *                                    *                            
      **************************************                            
       3000-FINAL.                                                      
           IF RETURN-CODE NOT EQUAL 9999                                
             PERFORM 3010-CLOSE-FILES                                   
                THRU 3010-CLOSE-FILES-F                                 
             PERFORM 3020-MOSTRAR-TOTALES                               
                THRU 3020-MOSTRAR-TOTALES-F                             
           END-IF.                                                      
       3000-FINAL-F. EXIT.                                              
      **************************************                            
      *   CERRAR ARCHIVOS                  *                            
      **************************************                            
       3010-CLOSE-FILES.                                                
           CLOSE NOVCLI                                                 
              IF FS-NOVCLI  IS NOT EQUAL '00'                           
                DISPLAY '* ERROR EN CLOSE SUCURSAL= ' FS-NOVCLI         
                MOVE 9999 TO RETURN-CODE                                
             END-IF                                                     
                                                                        
           CLOSE S1GRAB                                                 
              IF FS-S1GRAB   IS NOT EQUAL '00'                          
                DISPLAY '* ERROR EN CLOSE GRABNINO= ' FS-S1GRAB         
                MOVE 9999 TO RETURN-CODE                                
             END-IF                                                     
                                                                        
           CLOSE S2ERRR                                                 
              IF FS-S2ERRR   IS NOT EQUAL '00'                          
                DISPLAY '* ERROR EN CLOSE GRABNINO= ' FS-S2ERRR         
                MOVE 9999 TO RETURN-CODE                                
             END-IF                                                     
           .                                                            
       3010-CLOSE-FILES-F. EXIT.                                        
                                                                        
      **************************************                            
      *  MOSTRAR TOTALES                   *                            
      **************************************                            
       3020-MOSTRAR-TOTALES.                                            
           MOVE WS-CANT-LEIDOS TO WS-EDIT-LEIDOS                        
           MOVE WS-CANT-GRAB   TO WS-EDIT-GRAB                          
           MOVE WS-CANT-ERRR   TO WS-EDIT-ERRR                          
           DISPLAY WS-LINEA-SEPARACION                                  
           DISPLAY ' TOTAL DE ENTRADAS LEIDAS '  WS-EDIT-LEIDOS         
           DISPLAY ' TOTAL DE GRABADOS OK     '  WS-EDIT-GRAB           
           DISPLAY ' TOTAL DE GRABADOS NO OK  '  WS-EDIT-ERRR           
           DISPLAY WS-LINEA-SEPARACION.                                 
       3020-MOSTRAR-TOTALES-F. EXIT.                                    
                                                                        
      *----------------------------------------------------------------*
      * VALIDAR FECHA EN FORMATO AAAAMMDD                              *
      *----------------------------------------------------------------*
       4000-VALIDAR-FECHA.                                              
           MOVE 'S' TO WS-FECHA-VALIDA                                  
                                                                        
      *    EXTRAER COMPONENTES DE LA FECHA                              
           MOVE WS-FECHA-AAAAMMDD(1:4) TO WS-FECHA-AA                   
           MOVE WS-FECHA-AAAAMMDD(5:2) TO WS-FECHA-MM                   
           MOVE WS-FECHA-AAAAMMDD(7:2) TO WS-FECHA-DD                   
                                                                        
      *    VALIDAR EL MES                                               
           IF WS-FECHA-MM < 1 OR WS-FECHA-MM > 12                       
               MOVE 'N' TO WS-FECHA-VALIDA                              
           ELSE                                                         
               PERFORM 4100-VALIDAR-BISIESTO                            
                  THRU 4100-VALIDAR-BISIESTO-F                          
                                                                        
      *        VALIDAR DC DE LA FECHA DEL MES                           
               IF WS-FECHA-DD < 1 OR                                    
                  WS-FECHA-DD > WS-DIAS-MES(WS-FECHA-MM)                
                   MOVE 'N' TO WS-FECHA-VALIDA                          
               END-IF                                                   
           END-IF.                                                      
       4000-VALIDAR-FECHA-F. EXIT.                                      
                                                                        
       4100-VALIDAR-BISIESTO.                                           
      *    DETERMINAR SI ES ANIO BISIESTO                               
           MOVE ZEROS TO WS-RESTO-CUATRO                                
           MOVE ZEROS TO WS-RESTO-CIEN                                  
           MOVE ZEROS TO WS-RESTO-CUATROCIENTOS                         
                                                                        
           DIVIDE WS-FECHA-AA BY 4                                      
               GIVING WS-RESTO REMAINDER WS-RESTO-CUATRO.               
           DIVIDE WS-FECHA-AA BY 100                                    
               GIVING WS-RESTO REMAINDER WS-RESTO-CIEN.                 
           DIVIDE WS-FECHA-AA BY 400                                    
               GIVING WS-RESTO REMAINDER WS-RESTO-CUATROCIENTOS.        
                                                                        
      *    AJUSTAR DC AS DE FEBRERO SI ES BISIESTO                      
           IF WS-FECHA-MM = 2                                           
               IF (WS-RESTO-CUATRO = 0 AND WS-RESTO-CIEN NOT = 0) OR    
                  WS-RESTO-CUATROCIENTOS = 0                            
                   MOVE 29 TO WS-DIAS-MES(2)                            
               ELSE                                                     
                   MOVE 28 TO WS-DIAS-MES(2)                            
               END-IF                                                   
           END-IF.
           
       4100-VALIDAR-BISIESTO-F. EXIT.                                   
                                                                        
                                                                        
