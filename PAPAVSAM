       IDENTIFICATION DIVISION.
       PROGRAM-ID PAPAVSAM.
       AUTHOR.        D. CAZABAT.
       INSTALLATION.  CURSO CODEKI.
       DATE-WRITTEN.  16/05/2025.
       DATE-COMPILED. 16/05/2025.
       SECURITY.      UNCLASSIFIED.
      ***********************************************************
      *  PROGRAMA PARA VSAM KSDS                                *
      *  PROCESO DE APAREO DE MAESTRO DE CLIENTES CON NOVEDADES *
      *  Y GENERACIC0N DE ARCHIVO DE SALIDA CON ENCONTRADOS     *
      ***********************************************************
       ENVIRONMENT DIVISION.
       CONFIGURATION SECTION.
       SPECIAL-NAMES.
           DECIMAL-POINT IS COMMA.

       INPUT-OUTPUT SECTION.
       FILE-CONTROL.
           SELECT CLIENTES ASSIGN DDCLIEN
           ORGANIZATION IS INDEXED
           ACCESS IS SEQUENTIAL
           RECORD KEY IS CLI-TIP-DOC OF REG-CLIENTE
           ALTERNATE RECORD KEY IS CLI-NRO-DOC OF REG-CLIENTE
           FILE STATUS IS WS-FCLIENTE-CODE.
           *> Comentario de prueba

           SELECT MOVICC   ASSIGN DDMOVIC
           ACCESS IS SEQUENTIAL
           FILE STATUS IS WS-FMOVICC-CODE.

           SELECT SALCLIEN ASSIGN DDSCLIE
           ORGANIZATION IS SEQUENTIAL
           ACCESS IS SEQUENTIAL
           FILE STATUS IS WS-FSALCLIEN-CODE.

       DATA DIVISION.
       FILE SECTION.
       FD  CLIENTES.
           COPY CPCLIE.
       FD  MOVICC
            BLOCK CONTAINS 0 RECORDS
            RECORDING MODE IS F.
           COPY CPNOVCLI.

       FD  SALCLIEN
            BLOCK CONTAINS 0 RECORDS
            RECORDING MODE IS F.
           COPY CPCLIENS.

      **************************************
       WORKING-STORAGE SECTION.
      **************************************
       77  FILLER        PIC X(26) VALUE '* INICIO WORKING-STORAGE *'.
       77  FILLER        PIC X(26) VALUE '* CODIGOS RETORNO FILES  *'.
       77  WS-FCLIENTE-CODE     PIC XX    VALUE SPACES.
       77  WS-FMOVICC-CODE      PIC XX    VALUE SPACES.
       77  WS-FSALCLIEN-CODE    PIC XX    VALUE SPACES.

      * INDICADORES DE FIN DE ARCHIVO
       01  WS-STATUS-FIN    PIC X.
           88  WS-FIN-LECTURA         VALUE 'Y'.
           88  WS-NO-FIN-LECTURA      VALUE 'N'.

       01  WS-STATUS-CLIENTE   PIC X.
           88  WS-FIN-CLIENTE         VALUE 'Y'.
           88  WS-NO-FIN-CLIENTE      VALUE 'N'.

       01  WS-STATUS-MOVICC    PIC X.
           88  WS-FIN-MOVICC          VALUE 'Y'.
           88  WS-NO-FIN-MOVICC       VALUE 'N'.

      * CONTADORES
       01  WS-CONTADORES.
           03 WS-CONT-CLIENTES       PIC 9(06) VALUE ZEROS.
           03 WS-CONT-NOVEDADES      PIC 9(06) VALUE ZEROS.
           03 WS-CONT-ENCONTRADOS    PIC 9(06) VALUE ZEROS.
           03 WS-CONT-NO-ENCONTRADOS PIC 9(06) VALUE ZEROS.

       77  FILLER        PIC X(26) VALUE '* FINAL  WORKING-STORAGE *'.

      ***************************************************************.
       PROCEDURE DIVISION.
      **************************************
      *                                    *
      *  CUERPO PRINCIPAL DEL PROGRAMA     *
      *                                    *
      **************************************
       MAIN-PROGRAM.
           PERFORM 1000-INICIO
              THRU 1000-INICIO-EXIT
           PERFORM 2000-PROCESO
              THRU 2000-PROCESO-EXIT
              UNTIL WS-FIN-CLIENTE OR WS-FIN-MOVICC
           PERFORM 9000-FINAL
              THRU 9000-FINAL-EXIT.
       F-MAIN-PROGRAM. GOBACK.

      *
       1000-INICIO.
           INITIALIZE WS-CONTADORES
           SET WS-NO-FIN-CLIENTE TO TRUE
           SET WS-NO-FIN-MOVICC TO TRUE
           SET WS-NO-FIN-LECTURA TO TRUE

           OPEN INPUT CLIENTES
           IF WS-FCLIENTE-CODE NOT = '00'
              DISPLAY 'ERROR OPEN CLIENTES: ' WS-FCLIENTE-CODE
              SET WS-FIN-LECTURA TO TRUE
           END-IF

           OPEN INPUT MOVICC
           IF WS-FMOVICC-CODE NOT = '00'
              DISPLAY 'ERROR OPEN MOVICC: ' WS-FMOVICC-CODE
              SET WS-FIN-LECTURA TO TRUE
           END-IF

           OPEN OUTPUT SALCLIEN
           IF WS-FSALCLIEN-CODE NOT = '00'
              DISPLAY 'ERROR OPEN SALCLIEN: ' WS-FSALCLIEN-CODE
              SET WS-FIN-LECTURA TO TRUE
           END-IF

           PERFORM 3000-LEER-CLIENTE
              THRU 3000-LEER-CLIENTE-EXIT
           PERFORM 4000-LEER-MOVICC
              THRU 4000-LEER-MOVICC-EXIT.
       1000-INICIO-EXIT. EXIT.

       2000-PROCESO.
           EVALUATE TRUE
              WHEN CLI-TIP-CUE IN REG-CLIENTE =
                   NOV-CLI-TIPO IN WS-REG-NOVCLIE
                 AND CLI-NRO IN REG-CLIENTE =
                 NOV-CLI-NRO IN WS-REG-NOVCLIE
                    PERFORM 5000-PROCESAR-COINC
                       THRU 5000-PROCESAR-COINC-EXIT
                    PERFORM 3000-LEER-CLIENTE
                       THRU 3000-LEER-CLIENTE-EXIT
                    PERFORM 4000-LEER-MOVICC
                       THRU 4000-LEER-MOVICC-EXIT
               WHEN CLI-TIP-CUE IN REG-CLIENTE <
                    NOV-CLI-TIPO IN WS-REG-NOVCLIE
                    OR (CLI-TIP-CUE IN REG-CLIENTE =
                    NOV-CLI-TIPO IN WS-REG-NOVCLIE
                    AND CLI-NRO IN REG-CLIENTE <
                    NOV-CLI-NRO IN WS-REG-NOVCLIE)
                    PERFORM 3000-LEER-CLIENTE
                       THRU 3000-LEER-CLIENTE-EXIT
               WHEN OTHER
                    ADD 1 TO WS-CONT-NO-ENCONTRADOS
                    PERFORM 4000-LEER-MOVICC
                       THRU 4000-LEER-MOVICC-EXIT
           END-EVALUATE.
       2000-PROCESO-EXIT. EXIT.

       3000-LEER-CLIENTE.
           READ CLIENTES NEXT RECORD
             AT END
                 SET WS-FIN-CLIENTE TO TRUE
             NOT AT END
               IF WS-FCLIENTE-CODE = '00'
                 ADD 1 TO WS-CONT-CLIENTES
               ELSE
                 DISPLAY 'ERROR EN LECTURA CLIENTES: ' WS-FCLIENTE-CODE
                 SET WS-FIN-CLIENTE TO TRUE
               END-IF
           END-READ.
       3000-LEER-CLIENTE-EXIT. EXIT.

       4000-LEER-MOVICC.
           READ MOVICC
               AT END
                   SET WS-FIN-MOVICC TO TRUE
               NOT AT END
                   ADD 1 TO WS-CONT-NOVEDADES
           END-READ.
       4000-LEER-MOVICC-EXIT. EXIT.

       5000-PROCESAR-COINC.
           ADD 1 TO WS-CONT-ENCONTRADOS
           INITIALIZE REG-CLIENTES

           MOVE NOV-TIP-DOC IN WS-REG-NOVCLIE
             TO CLIS-TIP-DOC IN REG-CLIENTES
           MOVE NOV-NRO-DOC IN WS-REG-NOVCLIE
             TO CLIS-NRO-DOC IN REG-CLIENTES
           MOVE NOV-SUC IN WS-REG-NOVCLIE
             TO CLIS-SUC IN REG-CLIENTES

           MOVE NOV-CLI-TIPO IN WS-REG-NOVCLIE
             TO CLIS-TIPO IN REG-CLIENTES
           MOVE NOV-CLI-NRO IN WS-REG-NOVCLIE
             TO CLIS-NRO IN REG-CLIENTES
           MOVE NOV-CLI-IMP IN WS-REG-NOVCLIE
             TO CLIS-IMPORTE IN REG-CLIENTES
           MOVE NOV-CLI-FECHA IN WS-REG-NOVCLIE
             TO CLIS-AAAAMMDD IN REG-CLIENTES

           WRITE REG-CLIENTES
           IF WS-FSALCLIEN-CODE NOT = '00'
              DISPLAY 'ERROR WRITE SALCLIEN: ' WS-FSALCLIEN-CODE
           END-IF.
       5000-PROCESAR-COINC-EXIT. EXIT.

       9000-FINAL.
           CLOSE CLIENTES
           IF WS-FCLIENTE-CODE NOT = '00'
              DISPLAY 'ERROR CLOSE CLIENTES: ' WS-FCLIENTE-CODE
           END-IF

           CLOSE MOVICC
           IF WS-FMOVICC-CODE NOT = '00'
              DISPLAY 'ERROR CLOSE MOVICC: ' WS-FMOVICC-CODE
           END-IF

           CLOSE SALCLIEN
           IF WS-FSALCLIEN-CODE NOT = '00'
              DISPLAY 'ERROR CLOSE SALCLIEN: ' WS-FSALCLIEN-CODE
           END-IF

           DISPLAY '********* ESTADISTICAS DEL PROCESO **********'
           DISPLAY 'CANTIDAD CLIENTES LEIDOS    : '
                  WS-CONT-CLIENTES
           DISPLAY 'CANTIDAD NOVEDADES LEIDAS   : '
                  WS-CONT-NOVEDADES
           DISPLAY 'CANTIDAD NOV. ENCONTRADAS   : '
                  WS-CONT-ENCONTRADOS
           DISPLAY 'CANTIDAD NOV. NO ENCONTRADAS: '
                  WS-CONT-NO-ENCONTRADOS.
       9000-FINAL-EXIT. EXIT.
