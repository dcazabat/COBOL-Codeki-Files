       IDENTIFICATION DIVISION.
       PROGRAM-ID. PGMFBC7C.
       AUTHOR.        D. CAZABAT.
       INSTALLATION.  CURSO CODEKI.
       DATE-WRITTEN.  02/09/2025.
       DATE-COMPILED. 02/09/2025.
       SECURITY.      UNCLASSIFIED.                                             
      ********************************************************
      *                                                      *
      *  DOBLE CORTE DE CONTROL : DB2                        *
      *  ============================                        *
      *                                                      *
      *  LEE LOS REGISTROS DE TBCRUCTA ORDENADOS POR         *
      *  TIPCUEN Y SUCCUEN PARA REALIZAR LOS SUBTOTALES DE   *
      *  LOS SALDOS POR CADA CORTE DE CONTROL Y TOTAL GRAL   *
      *  LA SALIDA DEL PROGRAMA SERA DE 2 FORMAS:            *   
      *    1. IMPRESION EN PANTALLA                          *
      *    2. GENERACION DE UN ARCHIVO DE SALIDA             *  
      *                                                      *
      *  NOTA: OWNER DE LA TABLA ACTUALIZADO DE KC02803      *
      *                                                      *
      ********************************************************
       ENVIRONMENT DIVISION.
       CONFIGURATION SECTION.
       SPECIAL-NAMES.
           DECIMAL-POINT IS COMMA.

       INPUT-OUTPUT SECTION.
       FILE-CONTROL.
           SELECT SALIDA  ASSIGN DDSALE
           FILE STATUS IS SW-FS-SALIDA.

       DATA DIVISION.
       FILE SECTION.
       FD  SALIDA
           BLOCK CONTAINS 0 RECORDS
           RECORDING MODE IS F.

       01  REG-SALIDA      PIC X(132).

       WORKING-STORAGE SECTION.
       01  WS-CURRENT-DATE.
           05 WS-DATE.
              10 WS-YEAR              PIC 9(4).
              10 WS-MONTH             PIC 9(2).
              10 WS-DAY               PIC 9(2).
           05 WS-TIME.
              10 WS-HOUR              PIC 9(2).
              10 WS-MINUTE            PIC 9(2).
              10 WS-SECOND            PIC 9(2).
              10 WS-HUNDREDTH         PIC 9(2).
      
       01  WS-STATUS.
           05 SW-FS-SALIDA           PIC X(02).
              88 SW-FS-OK            VALUE '00'.
              88 SW-FS-EOF           VALUE '10'.
              88 SW-FS-NOK           VALUE '01' THRU '09'
                                           '11' THRU '99'.
       77  WS-LINEAS-PAG             PIC 99    VALUE ZEROS.
       77  WS-NUM-PAGINA             PIC 999   VALUE 1.
       77  WS-NUM-PAGINA-DISP        PIC 999.
       01  WS-FLAG-FIN               PIC X.
           88  WS-SI-PROCESO               VALUE ' '.
           88  WS-FIN-PROCESO              VALUE 'F'.

       01  WS-ESTADOS-ANTERIORES.
           05 WS-TIPCUEN-ANTERIOR      PIC X(02) VALUE SPACES.
           05 WS-SUCUEN-ANTERIOR       PIC S9(2)V USAGE COMP-3 VALUE 0.

       01  WS-CONTADORES-CORTE.
           05 WS-CONT-TIPCUEN            PIC 9(3) VALUE ZEROS.
           05 WS-CONT-TIPCUEN-DISP       PIC ZZZ.
           05 WS-CONT-SUCCUEN            PIC 9(3) VALUE ZEROS.
           05 WS-CONT-SUCCUEN-DISP       PIC ZZZ.
           05 WS-CONT-TOTALLEI           PIC 9(3) VALUE ZEROS.
           05 WS-CONT-TOTALLEI-DISP      PIC ZZZ.

       01  WS-SUMARIZADORAS-CORTE.
           05 WS-TOT-SALDOS-TIPCUEN      PIC S9(13)V99 COMP-3 VALUE 0.
           05 WS-TOT-SALDOS-TIPCUEN-DISP PIC -$$.$$$.$$9,99.
           05 WS-TOT-SALDOS-SUCCUEN      PIC S9(13)V99 COMP-3 VALUE 0.
           05 WS-TOT-SALDOS-SUCCUEN-DISP PIC -$$.$$$.$$9,99.
           05 WS-TOT-SALDOS              PIC S9(13)V99 COMP-3 VALUE 0.
           05 WS-TOT-SALDOS-DISP         PIC -$$.$$$.$$9,99.
      
       77  WS-TOT-GRABADOS              PIC 9(3)  VALUE ZEROS.
       77  WS-TOT-GRABADOS-DISP         PIC ZZZ.
       77  WS-SUCUEN-DISP               PIC 99.
       77  WS-TIPCUEN-DISP              PIC X(2).
       77  WS-CONT-IMPRESOS             PIC 9(3)  VALUE ZEROS.
       77  WS-CONT-IMPRESOS-DISP        PIC ZZZ.

       77  WS-NROCUEN-EDIT              PIC ZZZZ9.
       77  WS-SALDO-EDIT                PIC -$$.$$$.$$9,99.
       77  WS-NROCLI-EDIT               PIC ZZZ.

       01  WS-FECHA.
           03  WS-DIA   PIC 99.
           03  FILLER   PIC X         VALUE '-'.
           03  WS-MES   PIC 99.
           03  FILLER   PIC X         VALUE '-'.
           03  WS-ANIO  PIC 9999.

       01  WS-FECHA-FORMATEADA          PIC X(10).

       01  WS-HEADERS.
           05 WS-ASTERISCOS         PIC X(132) VALUE ALL '*'.
           05 WS-TITULO             PIC X(132) VALUE SPACES.
           05 WS-GUIONES            PIC X(132) VALUE ALL '-'.

       77  WS-DESC-TIPCUEN-01   PIC X(20) VALUE 'CAJA DE AHORROS'.
       77  WS-DESC-TIPCUEN-02   PIC X(20) VALUE 'CUENTAS CORRIENTES'.
       77  WS-DESC-TIPCUEN-03   PIC X(20) VALUE 'PLAZO FIJO'.
       77  WS-DESC-TIPO-ACTUAL  PIC X(20).


      * SQLCA COMMUNICATION AREA
           EXEC SQL INCLUDE SQLCA END-EXEC.

      * DCLGEN de la tabla
           EXEC SQL INCLUDE TBCURCTA END-EXEC.

      **********************************************
      * DECLARACION DE LAS VARIABLES HOST Y CURSOR *
      **********************************************
           EXEC SQL 
              DECLARE CURSORCLI CURSOR 
              FOR
                 SELECT * 
                 FROM KC02803.TBCURCTA 
                 WHERE FECSAL < '2025-06-05'
                 ORDER BY TIPCUEN, SUCUEN 
           END-EXEC.

       77  FILLER  PIC X(30) VALUE '* FINAL  WORKING-STORAGE *'.

      ***************************************************************.
       PROCEDURE DIVISION.
      **************************************
      *                                    *
      *  CUERPO PRINCIPAL DEL PROGRAMA     *
      *                                    *
      **************************************
       MAIN-PROGRAM.
           PERFORM 1000-I-INICIO.
           PERFORM 2000-I-PROCESO UNTIL WS-FIN-PROCESO.
           PERFORM 9999-I-FINAL.
           GOBACK.

      **************************************
      *                                    *
      *  CUERPO INICIO APERTURA ARCHIVOS   *
      *                                    *
      **************************************
       1000-I-INICIO.
           SET WS-SI-PROCESO TO TRUE.

           OPEN OUTPUT SALIDA.
           IF NOT SW-FS-OK
              DISPLAY '* ERROR EN OPEN SALIDA  = ' SW-FS-SALIDA
              MOVE 9999 TO RETURN-CODE
              SET  WS-FIN-PROCESO TO TRUE
           END-IF.

           PERFORM 1100-I-WRITE-DISPLAY
           PERFORM 1200-I-WRITE-SALIDA

           EXEC SQL
              OPEN CURSORCLI
           END-EXEC.

           EVALUATE SQLCODE
              WHEN 0
                 EXEC SQL
                    FETCH CURSORCLI
                    INTO :DCLTBCURCTA
                 END-EXEC
                 
                 EVALUATE SQLCODE
                    WHEN 0
                       ADD 1 TO WS-CONT-TOTALLEI
                       MOVE WS-TIPCUEN TO WS-TIPCUEN-ANTERIOR
                       MOVE WS-SUCUEN TO WS-SUCUEN-ANTERIOR
                       MOVE 1 TO WS-CONT-TIPCUEN
                       MOVE 1 TO WS-CONT-SUCCUEN
                       ADD WS-SALDO TO WS-TOT-SALDOS-TIPCUEN
                       ADD WS-SALDO TO WS-TOT-SALDOS-SUCCUEN
                       ADD WS-SALDO TO WS-TOT-SALDOS
                       PERFORM 1300-I-WRITE-TIPCUEN
                       PERFORM 1400-I-WRITE-SUCUEN
                       MOVE SPACES TO REG-SALIDA
                       MOVE WS-NROCUEN TO WS-NROCUEN-EDIT
                       MOVE WS-SALDO TO WS-SALDO-EDIT
                       MOVE WS-NROCLI TO WS-NROCLI-EDIT
                       MOVE WS-TIPCUEN TO REG-SALIDA(2:2)
                       MOVE WS-NROCUEN-EDIT TO REG-SALIDA(9:5)
                       MOVE WS-SALDO-EDIT TO REG-SALIDA(19:14)
                       MOVE WS-NROCLI-EDIT TO REG-SALIDA(37:3)
                       WRITE REG-SALIDA
                       ADD 1 TO WS-TOT-GRABADOS
                       ADD 1 TO WS-CONT-IMPRESOS
                       ADD 1 TO WS-LINEAS-PAG
                    WHEN +100
                       SET WS-FIN-PROCESO TO TRUE
                    WHEN OTHER
                       DISPLAY 'ERROR SQL: ' SQLCODE ' - LECTURA CURSOR'
                       MOVE 9999 TO RETURN-CODE
                       SET WS-FIN-PROCESO TO TRUE
                 END-EVALUATE
                 
              WHEN OTHER
                 DISPLAY 'ERROR SQL: ' SQLCODE ' - APERTURA CURSOR'
                 MOVE 9999 TO RETURN-CODE
                 SET WS-FIN-PROCESO TO TRUE
           END-EVALUATE.

      ***************************************
      *                                     *
      *  TITULOS DEL LA PAGINA DEL PROGRAMA *
      *  PARA DISPLAY EN PANTALLA           *
      *                                     *
      ***************************************

       1100-I-WRITE-DISPLAY.
           MOVE FUNCTION CURRENT-DATE TO WS-CURRENT-DATE
           DISPLAY WS-ASTERISCOS 
           DISPLAY 'SALIDA POR DISPLAY PROGRAMA PGMFBC7C'
           DISPLAY WS-GUIONES
           DISPLAY 'FECHA DE EJECUCION: ' 
              WS-YEAR '-' WS-MONTH '-' WS-DAY
           DISPLAY 'HORA DE EJECUCION:  ' 
              WS-HOUR ':' WS-MINUTE ':' WS-SECOND
           DISPLAY WS-ASTERISCOS.

      ***************************************
      *                                     *
      *  TITULOS DEL LA PAGINA DEL PROGRAMA *
      *  PARA ARCHIVO DE SALIDA             *
      *                                     *
      ***************************************

       1200-I-WRITE-SALIDA.
           MOVE FUNCTION CURRENT-DATE TO WS-CURRENT-DATE
           STRING WS-DAY '/' WS-MONTH '/' WS-YEAR
                  DELIMITED BY SIZE
                  INTO WS-FECHA-FORMATEADA
           
           MOVE WS-NUM-PAGINA TO WS-NUM-PAGINA-DISP
           MOVE SPACES TO WS-TITULO
           
           STRING WS-FECHA-FORMATEADA
                  DELIMITED BY SIZE
                  INTO WS-TITULO(1:10)
           
           STRING 'DETALLE DE CUENTAS'
                  DELIMITED BY SIZE
                  INTO WS-TITULO(50:18)
           
           STRING 'ALUMNO: C7C  PAG: ' WS-NUM-PAGINA-DISP
                  DELIMITED BY SIZE  
                  INTO WS-TITULO(100:25)
           
           WRITE REG-SALIDA FROM WS-GUIONES
           WRITE REG-SALIDA FROM WS-TITULO 
           WRITE REG-SALIDA FROM WS-GUIONES
           ADD 3 TO WS-LINEAS-PAG.

      ***************************************
      *                                     *
      *  SALTO DE PÁGINA                    *
      *                                     *
      ***************************************
       1250-SALTO-PAGINA.
           MOVE X'0C' TO REG-SALIDA
           WRITE REG-SALIDA
           MOVE ZEROS TO WS-LINEAS-PAG
           ADD 1 TO WS-NUM-PAGINA
           PERFORM 1200-I-WRITE-SALIDA.

       1300-I-WRITE-TIPCUEN.
           EVALUATE WS-TIPCUEN
              WHEN '01'
                 MOVE 'CAJA DE AHORROS' TO WS-DESC-TIPO-ACTUAL
              WHEN '02'
                 MOVE 'CUENTAS CORRIENTES' TO WS-DESC-TIPO-ACTUAL
              WHEN '03'
                 MOVE 'PLAZO FIJO' TO WS-DESC-TIPO-ACTUAL
              WHEN OTHER
                 MOVE 'TIPO DESCONOCIDO' TO WS-DESC-TIPO-ACTUAL
           END-EVALUATE
           IF WS-LINEAS-PAG > 10
              PERFORM 1250-SALTO-PAGINA
           END-IF
           MOVE WS-TIPCUEN TO WS-TIPCUEN-DISP
           DISPLAY WS-GUIONES
           DISPLAY 'TIPO DE CUENTA: ' WS-TIPCUEN-DISP ' - ' 
                   WS-DESC-TIPO-ACTUAL
           DISPLAY WS-GUIONES
           MOVE SPACES TO REG-SALIDA
           STRING 'TIPO DE CUENTA: ' WS-TIPCUEN-DISP ' - ' 
                  WS-DESC-TIPO-ACTUAL
                  DELIMITED BY SIZE
                  INTO REG-SALIDA
           WRITE REG-SALIDA
      *     WRITE REG-SALIDA FROM WS-GUIONES
           ADD 2 TO WS-LINEAS-PAG.

       1400-I-WRITE-SUCUEN.
           IF WS-SUCUEN >= 0
              MOVE WS-SUCUEN TO WS-SUCUEN-DISP
           ELSE
              MOVE 0 TO WS-SUCUEN-DISP
           END-IF
           MOVE SPACES TO REG-SALIDA
           STRING 'SUCURSAL: ' WS-SUCUEN-DISP
                  DELIMITED BY SIZE
                  INTO REG-SALIDA
           WRITE REG-SALIDA
           MOVE SPACES TO REG-SALIDA
           STRING 'TIPO  NRO.CUENTA  SALDO           NRO.CLIENTE'
                  DELIMITED BY SIZE
                  INTO REG-SALIDA
           WRITE REG-SALIDA
           MOVE SPACES TO REG-SALIDA
           STRING '----  ----------  --------------  -----------'
                  DELIMITED BY SIZE
                  INTO REG-SALIDA
           WRITE REG-SALIDA
           ADD 3 TO WS-LINEAS-PAG.

      **************************************
      *                                    *
      *  CUERPO PRINCIPAL DEL PROGRAMA     *
      *                                    *
      **************************************
       2000-I-PROCESO.
           EXEC SQL
              FETCH CURSORCLI
              INTO :DCLTBCURCTA
           END-EXEC

           EVALUATE SQLCODE
              WHEN 0
                 ADD 1 TO WS-CONT-TOTALLEI
                 IF WS-TIPCUEN NOT = WS-TIPCUEN-ANTERIOR
                    IF WS-SUCUEN-ANTERIOR NOT = 0
                       PERFORM 2200-CORTE-SUCUEN
                    END-IF
                    IF WS-TIPCUEN-ANTERIOR NOT = SPACES
                       PERFORM 2100-CORTE-TIPCUEN
                    END-IF
                    MOVE 0 TO WS-CONT-TIPCUEN
                    MOVE 0 TO WS-TOT-SALDOS-TIPCUEN
                    MOVE WS-TIPCUEN TO WS-TIPCUEN-ANTERIOR
                    PERFORM  1300-I-WRITE-TIPCUEN
                    MOVE 0 TO WS-SUCUEN-ANTERIOR
                    MOVE 0 TO WS-CONT-SUCCUEN
                    MOVE 0 TO WS-TOT-SALDOS-SUCCUEN
                 END-IF
                 IF WS-SUCUEN NOT = WS-SUCUEN-ANTERIOR
                    IF WS-SUCUEN-ANTERIOR NOT = 0
                       PERFORM 2200-CORTE-SUCUEN
                    END-IF
                    MOVE 0 TO WS-CONT-SUCCUEN
                    MOVE 0 TO WS-TOT-SALDOS-SUCCUEN
                    MOVE WS-SUCUEN TO WS-SUCUEN-ANTERIOR
                    PERFORM 1400-I-WRITE-SUCUEN
                 END-IF
                 ADD 1 TO WS-CONT-TIPCUEN
                 ADD 1 TO WS-CONT-SUCCUEN
                 ADD WS-SALDO  TO WS-TOT-SALDOS-TIPCUEN
                 ADD WS-SALDO  TO WS-TOT-SALDOS-SUCCUEN
                 ADD WS-SALDO  TO WS-TOT-SALDOS
                 MOVE SPACES TO REG-SALIDA
                 MOVE WS-NROCUEN TO WS-NROCUEN-EDIT
                 MOVE WS-SALDO TO WS-SALDO-EDIT
                 MOVE WS-NROCLI TO WS-NROCLI-EDIT
                 MOVE WS-TIPCUEN TO REG-SALIDA(2:2)
                 MOVE WS-NROCUEN-EDIT TO REG-SALIDA(9:5)
                 MOVE WS-SALDO-EDIT TO REG-SALIDA(19:14)
                 MOVE WS-NROCLI-EDIT TO REG-SALIDA(37:3)
                 WRITE REG-SALIDA
                 ADD 1 TO WS-TOT-GRABADOS
                 ADD 1 TO WS-CONT-IMPRESOS
                 ADD 1 TO WS-LINEAS-PAG
                 IF WS-LINEAS-PAG > 15
                    PERFORM 1250-SALTO-PAGINA
                 END-IF
              WHEN +100
                 IF WS-CONT-TOTALLEI > 0
                    IF WS-SUCUEN-ANTERIOR NOT = 0
                       PERFORM 2200-CORTE-SUCUEN
                    END-IF
                    IF WS-TIPCUEN-ANTERIOR NOT = SPACES
                       PERFORM 2100-CORTE-TIPCUEN
                    END-IF
                 END-IF
                 SET WS-FIN-PROCESO TO TRUE
              WHEN OTHER
                 DISPLAY 'ERROR SQL: ' SQLCODE
                 SET WS-FIN-PROCESO TO TRUE
           END-EVALUATE.

      **************************************
      *                                    *
      *  CORTE POR TIPO DE CUENTA          *
      *                                    *
      **************************************
       2100-CORTE-TIPCUEN.
           DISPLAY WS-GUIONES
           MOVE WS-TOT-SALDOS-TIPCUEN TO WS-TOT-SALDOS-TIPCUEN-DISP
           MOVE WS-CONT-TIPCUEN TO WS-CONT-TIPCUEN-DISP
           
           MOVE WS-TIPCUEN-ANTERIOR TO WS-TIPCUEN-DISP
           DISPLAY 'TOTAL TIPO CUENTA ' WS-TIPCUEN-DISP ': ' 
                  WS-TOT-SALDOS-TIPCUEN-DISP
           DISPLAY 'CANTIDAD DE CUENTAS: ' WS-CONT-TIPCUEN-DISP
           DISPLAY WS-GUIONES

           MOVE SPACES TO REG-SALIDA
           STRING 'TOTAL TIPO CUENTA ' 
                  WS-TIPCUEN-DISP
                  ': '
                  WS-TOT-SALDOS-TIPCUEN-DISP
                  DELIMITED BY SIZE
                  INTO REG-SALIDA
           WRITE REG-SALIDA
           
           MOVE SPACES TO REG-SALIDA
           STRING 'CANTIDAD DE CUENTAS: ' 
                  WS-CONT-TIPCUEN-DISP
                  DELIMITED BY SIZE
                  INTO REG-SALIDA
           WRITE REG-SALIDA
           
           WRITE REG-SALIDA FROM WS-GUIONES
           ADD 3 TO WS-LINEAS-PAG.

      **************************************
      *                                    *
      *  CORTE POR SUCURSAL                *
      *                                    *
      **************************************
       2200-CORTE-SUCUEN.
           MOVE WS-TOT-SALDOS-SUCCUEN TO WS-TOT-SALDOS-SUCCUEN-DISP
           MOVE WS-CONT-SUCCUEN TO WS-CONT-SUCCUEN-DISP
           
           IF WS-SUCUEN-ANTERIOR >= 0
              MOVE WS-SUCUEN-ANTERIOR TO WS-SUCUEN-DISP
           ELSE
              MOVE 0 TO WS-SUCUEN-DISP
           END-IF
           DISPLAY 'SUCURSAL: ' WS-SUCUEN-DISP ' // CLIENTES : ' 
                  WS-CONT-SUCCUEN-DISP ' // TOTAL : ' 
                  WS-TOT-SALDOS-SUCCUEN-DISP.
           
           WRITE REG-SALIDA FROM WS-GUIONES
           MOVE SPACES TO REG-SALIDA
           STRING '  TOTAL SUCURSAL ' 
                  WS-SUCUEN-DISP 
                  ': '
                  WS-TOT-SALDOS-SUCCUEN-DISP
                  DELIMITED BY SIZE
                  INTO REG-SALIDA
           WRITE REG-SALIDA
           
           MOVE SPACES TO REG-SALIDA
           STRING '  CANTIDAD DE CUENTAS: ' 
                  WS-CONT-SUCCUEN-DISP
                  DELIMITED BY SIZE
                  INTO REG-SALIDA
           WRITE REG-SALIDA
           
           WRITE REG-SALIDA FROM WS-GUIONES
           ADD 4 TO WS-LINEAS-PAG.

      **************************************
      *                                    *
      *  CUERPO FINAL y CIERRE DE FILES    *
      *                                    *
      **************************************
       9999-I-FINAL.
           DISPLAY ' '
           DISPLAY WS-ASTERISCOS
           DISPLAY 'TOTAL GENERAL POR TIPO DE CUENTA'
           DISPLAY WS-GUIONES
           MOVE WS-TOT-SALDOS TO WS-TOT-SALDOS-DISP
           DISPLAY 'TOTAL GENERAL DE SALDOS: ' WS-TOT-SALDOS-DISP
           DISPLAY WS-GUIONES
           DISPLAY WS-ASTERISCOS
           DISPLAY 'TOTALES ESTADÍSTICOS FINALES'
           DISPLAY WS-GUIONES
           MOVE WS-CONT-TOTALLEI TO WS-CONT-TOTALLEI-DISP
           DISPLAY 'REGISTROS LEÍDOS: ' WS-CONT-TOTALLEI-DISP
           MOVE WS-CONT-IMPRESOS TO WS-CONT-IMPRESOS-DISP
           DISPLAY 'REGISTROS IMPRESOS: ' WS-CONT-IMPRESOS-DISP
           DISPLAY WS-GUIONES
           DISPLAY WS-ASTERISCOS
           MOVE SPACES TO REG-SALIDA
           STRING 'RESUMEN FINAL DE TOTALES'
                  DELIMITED BY SIZE
                  INTO REG-SALIDA
           WRITE REG-SALIDA
           WRITE REG-SALIDA FROM WS-GUIONES
           MOVE WS-TOT-SALDOS TO WS-TOT-SALDOS-DISP
           MOVE SPACES TO REG-SALIDA
           STRING 'SALDOS TOTALES: ' WS-TOT-SALDOS-DISP
                  DELIMITED BY SIZE
                  INTO REG-SALIDA
           WRITE REG-SALIDA
           MOVE SPACES TO REG-SALIDA
           STRING 'REGISTROS LEIDOS: ' WS-CONT-TOTALLEI-DISP
                  DELIMITED BY SIZE
                  INTO REG-SALIDA
           WRITE REG-SALIDA
           MOVE WS-CONT-IMPRESOS TO WS-CONT-IMPRESOS-DISP
           MOVE SPACES TO REG-SALIDA
           STRING 'CANTIDAD REGISTROS IMPRESOS: ' WS-CONT-IMPRESOS-DISP
                  DELIMITED BY SIZE
                  INTO REG-SALIDA
           WRITE REG-SALIDA
           WRITE REG-SALIDA FROM WS-ASTERISCOS
           EXEC SQL
              CLOSE CURSORCLI
           END-EXEC.
           EVALUATE SQLCODE
              WHEN 0
                 CONTINUE
              WHEN OTHER
                 DISPLAY 'ERROR SQL: ' SQLCODE ' - CIERRE CURSOR'
                 MOVE 9999 TO RETURN-CODE
           END-EVALUATE.
           CLOSE SALIDA
           IF NOT SW-FS-OK
              DISPLAY '* ERROR EN CLOSE SALIDA  = ' SW-FS-SALIDA
              MOVE 9999 TO RETURN-CODE
           END-IF.
           EXIT.
