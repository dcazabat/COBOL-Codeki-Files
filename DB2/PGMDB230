       IDENTIFICATION DIVISION.
       PROGRAM-ID. PGMDB230.
       AUTHOR.        D. CAZABAT.
       INSTALLATION.  CURSO CODEKI.
       DATE-WRITTEN.  20/06/2025.
       DATE-COMPILED. 20/06/2025.
       SECURITY.      UNCLASSIFIED.                                             
      ********************************************************
      *                                                      *
      *  SINCRONICA 30: DB2                                  *
      *  ============================                        *
      *                                                      *
      *  DEBERÁN GENERAR LAS NOVEDADES VALIDADAS EN ARCHIVO: *
      *  KC03XXXX.NOVECLI que ingresa al JCL EJENOVKS        *
      *  KC02788.ALU9999.COPYLIB(TBVCLIENT)                  *
      *                                                      *
      *  SE AGREGA EL LLAMADO A UN RUTINA DE VALIDACION      *
      *  DE FECHA LLAMADA PGMRUC7C                           *
      *  QUE DEBE SER IMPLEMENTADA POR EL ALUMNO.            *
      ********************************************************
       ENVIRONMENT DIVISION.
       CONFIGURATION SECTION.

       SPECIAL-NAMES.
           DECIMAL-POINT IS COMMA.
           
       INPUT-OUTPUT SECTION.
       FILE-CONTROL.
           SELECT ENTRADA
               ASSIGN TO NOVECLI
               ORGANIZATION IS INDEXED
               ACCESS MODE IS SEQUENTIAL
               RECORD KEY IS REG-KEY
               FILE STATUS IS SW-FS-ENTRADA.

       DATA DIVISION.
       FILE SECTION.
       FD  ENTRADA.
       01 REG-KEY.
           03 K-WK-CLI-TIPO-NOVEDAD    PIC X(02).
           03 K-WK-CLI-TIPO-DOCUMENTO  PIC X(02).
           03 K-WK-CLI-NRO-DOCUMENTO   PIC 9(11).
           03 K-WK-CLI-NRO-SEC         PIC 9(02).
           COPY TBVCLIEN.

       WORKING-STORAGE SECTION.
      * Variables de fecha y hora
       01  WS-CURRENT-DATE.
           05 WS-DATE.
              10 WS-YEAR              PIC 9(4).
              10 WS-MONTH             PIC 9(2).
              10 WS-DAY               PIC 9(2).
           05 WS-TIME.
              10 WS-HOUR              PIC 9(2).
              10 WS-MINUTE            PIC 9(2).
              10 WS-SECOND            PIC 9(2).
              10 WS-HUNDREDTH         PIC 9(2).
      * Variables de control
       01  WS-STATUS.
           05 SW-FS-ENTRADA           PIC X(02).
              88 SW-FS-OK             VALUE '00'.
              88 SW-FS-EOF            VALUE '10'.
              88 SW-FS-NOK            VALUE '01' THRU '09'
                                          '11' THRU '99'.
           05 WS-CONTADORES.
              10 WS-LEIDOS        PIC 9(06) VALUE ZEROS.
              10 WS-INSERTADOS    PIC 9(06) VALUE ZEROS.
              10 WS-ERRORES       PIC 9(06) VALUE ZEROS.

       01  WS-HEADERS.
           05 WS-HEAD1             PIC X(50) VALUE ALL '*'.
           05 WS-HEAD2             PIC X(50) VALUE 
              'LISTADO DE NOVEDADES DE CLIENTES'.
           05 WS-HEAD3             PIC X(50) VALUE ALL '-'.

      * DCLGEN de la tabla
           EXEC SQL INCLUDE TBCURCLI END-EXEC.
      * SQLCA COMMUNICATION AREA
           EXEC SQL INCLUDE SQLCA END-EXEC.

       01  LK-AREA.
           03 FILLER.
              05 LK-SIGLO    PIC 99.
              05 LK-ANIO     PIC 99.
           03 LK-MES      PIC 99.
           03 LK-DIA      PIC 99.
           03 FILLER      PIC X(22).

       PROCEDURE DIVISION.
       0000-MAIN.
           PERFORM 1000-INICIO
           PERFORM 2000-PROCESO
           PERFORM 3000-FINAL
           GOBACK.

       1000-INICIO.
           INITIALIZE WS-CONTADORES

           *> Obtener fecha y hora actual
           *> y formatear para visualización
           MOVE FUNCTION CURRENT-DATE TO WS-CURRENT-DATE
           DISPLAY WS-HEAD1 
           DISPLAY WS-HEAD2
           DISPLAY 'FECHA DE EJECUCION: ' 
              WS-YEAR '-' WS-MONTH '-' WS-DAY
           DISPLAY 'HORA DE EJECUCION:  ' 
              WS-HOUR ':' WS-MINUTE ':' WS-SECOND
           DISPLAY WS-HEAD1 

           *> Abrir archivo de entrada
           OPEN INPUT ENTRADA
           IF NOT SW-FS-OK
              DISPLAY 'ERROR AL ABRIR ARCHIVO DE ENTRADA: ' 
                       SW-FS-ENTRADA
              MOVE 9999 TO RETURN-CODE
              GOBACK
           END-IF
           PERFORM 2100-LEER-ENTRADA.

       2000-PROCESO.
           PERFORM UNTIL SW-FS-EOF
              PERFORM 2200-INSERTAR-REGISTRO
              PERFORM 2100-LEER-ENTRADA
           END-PERFORM.

       2100-LEER-ENTRADA.
           READ ENTRADA
           AT END 
              SET SW-FS-EOF TO TRUE
           NOT AT END
              ADD 1 TO WS-LEIDOS
           END-READ
           IF SW-FS-NOK
              DISPLAY 'ERROR LECTURA ARCHIVO: ' SW-FS-ENTRADA
              MOVE 9999 TO RETURN-CODE
              GOBACK
           END-IF.

       2200-INSERTAR-REGISTRO.
           INITIALIZE DCLTBCURCLI
           MOVE WK-CLI-TIPO-DOCUMENTO TO WT-TIPDOC
           MOVE WK-CLI-NRO-DOCUMENTO  TO WT-NRODOC
           MOVE WK-CLI-NRO-CLIENTE    TO WT-NROCLI
           STRING WK-CLI-NOMBRE-CLIENTE DELIMITED BY SIZE
                  ' '                  DELIMITED BY SIZE
                  WK-CLI-APELLIDO-CLIENTE DELIMITED BY SIZE
                  INTO WT-NOMAPE
           MOVE WK-CLI-FECHA-NACIMIENTO TO WT-FECNAC
           MOVE WK-CLI-SEXO(1:1)        TO WT-SEXO

           *> Preparar datos para PGMRUC7C
           MOVE WK-CLI-FECHA-NACIMIENTO(1:2) TO LK-SIGLO
           MOVE WK-CLI-FECHA-NACIMIENTO(3:2) TO LK-ANIO
           MOVE WK-CLI-FECHA-NACIMIENTO(5:2) TO LK-MES
           MOVE WK-CLI-FECHA-NACIMIENTO(7:2) TO LK-DIA

           CALL 'PGMRUC7C' USING LK-AREA

           IF RETURN-CODE = 05
              DISPLAY 'ERROR EN VALIDACION DE FECHA - DOC: ' 
                      WK-CLI-NRO-DOCUMENTO
              ADD 1 TO WS-ERRORES
           ELSE
              *> Validar el código de retorno antes del INSERT
              IF RETURN-CODE = ZEROS
                 *> Construir la fecha en formato AAAA-MM-DD
                 STRING LK-SIGLO LK-ANIO '-' LK-MES '-' LK-DIA
                        DELIMITED BY SIZE
                        INTO WT-FECNAC

                 EXEC SQL
                    INSERT INTO KC02787.TBCURCLI
                      (TIPDOC, NRODOC, NROCLI, NOMAPE, FECNAC, SEXO)
                    VALUES
                      (:WT-TIPDOC, :WT-NRODOC, :WT-NROCLI, 
                       :WT-NOMAPE, :WT-FECNAC, :WT-SEXO)
                 END-EXEC

                 EVALUATE SQLCODE
                    WHEN 0
                       ADD 1 TO WS-INSERTADOS
                    WHEN -803
                       ADD 1 TO WS-ERRORES
                       DISPLAY 'ERROR: CLAVE DUPLICADA - DOC: ' 
                               WK-CLI-NRO-DOCUMENTO
                    WHEN +100
                       ADD 1 TO WS-ERRORES
                       DISPLAY 'REGISTRO YA EXISTE - DOC: ' 
                               WK-CLI-NRO-DOCUMENTO
                    WHEN OTHER
                       ADD 1 TO WS-ERRORES
                       DISPLAY 'ERROR SQL: ' SQLCODE ' - DOC: ' 
                               WK-CLI-NRO-DOCUMENTO
                 END-EVALUATE
              ELSE
                 *> Codigo de error no contemplado
                 DISPLAY 'ERROR EN VALIDACION DE FECHA - DOC: ' 
                      WK-CLI-NRO-DOCUMENTO
                 ADD 1 TO WS-ERRORES
              END-IF
           END-IF.
|
       3000-FINAL.
           CLOSE ENTRADA
           DISPLAY WS-HEAD1
           DISPLAY WS-HEAD3
           DISPLAY 'RESUMEN DEL PROCESAMIENTO'
           DISPLAY WS-HEAD3
           DISPLAY 'CANTIDAD NOVEDADES LEIDAS     : ' WS-LEIDOS
           DISPLAY 'CANTIDAD NOVEDADES INSERTADAS : ' WS-INSERTADOS
           DISPLAY 'CANTIDAD NOVEDADES ERRONEAS   : ' WS-ERRORES
           DISPLAY WS-HEAD3.

