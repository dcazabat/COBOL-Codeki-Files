       IDENTIFICATION DIVISION.                                         
       PROGRAM-ID PGMCORTE.                                             
      **********************************************************        
      *                                                        *        
      *  PROGRAMA PARA EMITIR TOTAL CTAS ACTIVAS POR SUCURSAL  *        
      *  ACTIVAS POR SUC Y  TOTAL CTAS ACTIVAS BCO             *        
      *  LA ENTRADA CLASIFICADA X SUCURSAL                     *        
      *                                                        *        
      **********************************************************        
      *      MANTENIMIENTO DE PROGRAMA                         *        
      **********************************************************        
      *  FECHA   *    DETALLE        * COD *                            
      **************************************                            
      *          *                   *     *                            
      *          *                   *     *                            
      **************************************                            
       ENVIRONMENT DIVISION.                                            
       INPUT-OUTPUT SECTION.                                            
       FILE-CONTROL.                                                    
             SELECT ENTRADA ASSIGN DDENTRA                              
                    FILE STATUS IS WS-ENT-CODE.                         
                                                                        
             SELECT SALIDA  ASSIGN DDSALID                              
                    FILE STATUS IS WS-SAL-CODE.                         
                                                                        
       DATA DIVISION.                                                   
       FILE SECTION.                                                    
       FD ENTRADA                                                       
            BLOCK CONTAINS 0 RECORDS                                    
            RECORDING MODE IS F.                                        
                                                                        
       01 REG-ENTRADA    PIC X(30).                                     
                                                                        
       FD SALIDA                                                        
            BLOCK CONTAINS 0 RECORDS                                    
            RECORDING MODE IS F.                                        
                                                                        
       01 REG-SALIDA     PIC X(80).                                     
                                                                        
      **************************************                            
       WORKING-STORAGE SECTION.                                         
      **************************************                            
       77  FILLER        PIC X(26) VALUE '* INICIO WORKING-STORAGE *'.  
       77  FILLER        PIC X(26) VALUE '* CODIGOS RETORNO FILES  *'.  
       77  WS-ENT-CODE      PIC XX    VALUE SPACES.                     
       77  WS-SAL-CODE      PIC XX    VALUE SPACES.                     
                                                                        
       01  WS-STATUS-FIN    PIC X.                                      
           88  WS-FIN-LECTURA         VALUE 'Y'.                        
           88  WS-NO-FIN-LECTURA      VALUE 'N'.                        
                                                                        
       77  FILLER       PIC X(26) VALUE '* LAYOUT ENTRADA MESES   *'.   
      **************************************                            
               COPY CPSUCU.                                             
      **************************************                            
                                                                        
      **************************************                            
      * LAY-OUT GENERAL REGISTRO DE SALIDA                              
      **************************************                            
       01  WS-REG-SALIDA.                                               
           03  WS-SAL-SUCURSAL         PIC 99      VALUE ZEROS.         
           03  WS-SAL-NOMBRE-SUC       PIC X(30)   VALUE SPACES.        
           03  WS-SAL-CANT-CUENTAS     PIC ZZZ9    VALUE ZEROS.         
           03  FILLER                  PIC X(44)   VALUE SPACES.        
                                                                        
      **************************************                            
      * LAY-OUT ULTIMO REGISTRO DE SALIDA                               
      **************************************                            
                                                                        
       01  FILLER        REDEFINES    WS-REG-SALIDA.                    
           03  WS-REG-SALIDA-L.                                         
             05  WS-SAL-FECHA.                                          
                 07 WS-SAL-DD         PIC 99.                           
                 07 WS-SAL-GUION-DD   PIC X.                            
                 07 WS-SAL-MM         PIC 99.                           
                 07 WS-SAL-GUION-MM   PIC X.                            
                 07 WS-SAL-SIGLO      PIC   99.                         
                 07 WS-SAL-AA         PIC   99.                         
             05  WS-SAL-TITULO        PIC X(30).                        
             05  WS-SAL-TOTAL         PIC ZZZ9.                         
             05  FILLER               PIC X(36).                        
                                                                        
                                                                        
      ********  TOTAL CTAS X SUCURSAL ******                            
       77  WS-SUCURSAL-ANT      PIC 9(3)        VALUE     ZEROS.        
       77  WS-NOMBRE-SUC-ANT    PIC X(30)       VALUE     SPACES.       
       77  WS-CANT-CTAS-SUC     PIC 9(3)        VALUE     ZEROS.        
       77  WS-EDIT-LEIDAS       PIC ZZZ9        VALUE     ZEROS.        
       77  WS-CANT-LEIDAS       PIC 9(4)        VALUE     ZEROS.        
      ********  TOTAL CTAS X BANCO    ******                            
       77  WS-CANT-CTAS-BCO     PIC 9(4)        VALUE     ZEROS.        
      ********     CONSTANTES       ***************                     
       77  WS-TOTAL             PIC X(30)                               
                      VALUE    ' TOTAL CUENTAS ACTIVAS BANCO'.          
       77  WS-GUION             PIC X     VALUE '-'.                    
                                                                        
      ********     FECHA DE PROCESO ***************                     
       01  WS-FECHA.                                                    
           03  WS-FECHA-AA      PIC 99            VALUE ZEROS.          
           03  WS-FECHA-MM      PIC 99            VALUE ZEROS.          
           03  WS-FECHA-DD      PIC 99            VALUE ZEROS.          
                                                                        
       77  FILLER        PIC X(26) VALUE '* FINAL  WORKING-STORAGE *'.  
                                                                        
      ***************************************************************.  
       PROCEDURE DIVISION.                                              
      **************************************                            
      *                                    *                            
      *  CUERPO PRINCIPAL DEL PROGRAMA     *                            
      *                                    *                            
      **************************************                            
       MAIN-PROGRAM.                                                    
                                                                        
           PERFORM 1000-INICIO  THRU   F-1000-INICIO.                   
                                                                        
           PERFORM 2000-PROCESO  THRU  F-2000-PROCESO                   
                   UNTIL WS-FIN-LECTURA.                                
                                                                        
                                                                        
           PERFORM 9999-FINAL    THRU  F-9999-FINAL.                    
                                                                        
       F-MAIN-PROGRAM. GOBACK.                                          
                                                                        
      **************************************                            
      *                                    *                            
      *  CUERPO INICIO APERTURA ARCHIVOS   *                            
      *                                    *                            
      **************************************                            
       1000-INICIO.                                                     
           ACCEPT WS-FECHA FROM DATE.                                   
           DISPLAY 'FECHA:    '  WS-FECHA                               
                                                                        
                                                                        
           SET WS-NO-FIN-LECTURA TO TRUE.                               
                                                                        
           OPEN INPUT  ENTRADA.                                         
           IF WS-ENT-CODE IS NOT EQUAL '00'                             
              DISPLAY '* ERROR EN OPEN ENTRADA = ' WS-ENT-CODE          
              MOVE 9999 TO RETURN-CODE                                  
              SET  WS-FIN-LECTURA TO TRUE                               
           END-IF.                                                      
                                                                        
           OPEN OUTPUT SALIDA.                                          
           IF WS-SAL-CODE IS NOT EQUAL '00'                             
              DISPLAY '* ERROR EN OPEN SALIDA  = ' WS-SAL-CODE          
              MOVE 9999 TO RETURN-CODE                                  
              SET  WS-FIN-LECTURA TO TRUE                               
           END-IF.                                                      
                                                                        
      **************************************                            
      *  PRIMER LECTURA PARA CORTE CONTROL *                            
      **************************************                            
                                                                        
           READ ENTRADA INTO WS-REG-ENTRADA                             
                        AT END SET WS-FIN-LECTURA TO TRUE.              
                                                                        
           EVALUATE WS-ENT-CODE                                         
             WHEN '00'                                                  
                 MOVE WS-NRO-SUCURSAL     TO WS-SUCURSAL-ANT            
                 MOVE WS-NOMBRE-SUC       TO WS-NOMBRE-SUC-ANT          
                 ADD 1 TO WS-CANT-CTAS-SUC                              
                          WS-CANT-LEIDAS                                
                                                                        
              WHEN '10'                                                 
              DISPLAY '* ARCHIVO ENTRADA VAC�O      ' WS-ENT-CODE       
              MOVE 9999 TO RETURN-CODE                                  
              SET WS-FIN-LECTURA TO TRUE                                
                                                                        
           WHEN OTHER                                                   
              DISPLAY '* ERROR EN LECTURA ENTRADA = ' WS-ENT-CODE       
              MOVE 9999 TO RETURN-CODE                                  
              SET WS-FIN-LECTURA TO TRUE                                
                                                                        
           END-EVALUATE.                                                
                                                                        
       F-1000-INICIO.   EXIT.                                           
                                                                        
      **************************************                            
      *                                    *                            
      *  CUERPO PRINCIPAL DE PROCESOS      *                            
      *  LECTURA FILE INPUT CLASIFICADO    *                            
      *  GRABACION SALIDA CON LOS CAMPOS   *                            
      *  DE LAY-OUT GENERAL                *                            
      *                                    *                            
      **************************************                            
       2000-PROCESO.                                                    
                                                                        
           EVALUATE  WS-ESTADO-CUENTA                                   
               WHEN  ZERO                                               
                  ADD 1 TO WS-CANT-CTAS-SUC                             
                                                                        
               WHEN OTHER                                               
                  CONTINUE                                              
                                                                        
           END-EVALUATE                                                 
                                                                        
           READ ENTRADA INTO WS-REG-ENTRADA                             
                        AT END SET WS-FIN-LECTURA TO TRUE.              
                                                                        
           EVALUATE WS-ENT-CODE                                         
             WHEN '00'                                                  
                                                                        
           ADD 1   TO     WS-CANT-LEIDAS                                
           PERFORM 2200-SEGUIR-PROCESO                                  
                      THRU F-2200-SEGUIR-PROCESO                        
                                                                        
              WHEN '10'                                                 
              SET WS-FIN-LECTURA TO TRUE                                
              PERFORM 2100-ULTIMO-REGISTRO                              
                THRU  F-2100-ULTIMO-REGISTRO                            
                                                                        
           WHEN OTHER                                                   
              DISPLAY '* ERROR EN LECTURA ENTRADA = ' WS-ENT-CODE       
              MOVE 9999 TO RETURN-CODE                                  
              SET WS-FIN-LECTURA TO TRUE                                
                                                                        
           END-EVALUATE.                                                
                                                                        
                                                                        
       F-2000-PROCESO. EXIT.                                            
                                                                        
      **************************************                            
      *                                    *                            
      *  WRITE ULTIMO REGISTRO DE SALIDA   *                            
      *                                    *                            
      **************************************                            
       2100-ULTIMO-REGISTRO.                                            
                                                                        
           PERFORM 2230-CORTE-SUCURSAL                                  
                     THRU F-2230-CORTE-SUCURSAL                         
                                                                        
      *CAMBIE WS-REG-SALIDA X WS-REG-SALIDA-L                           
           INITIALIZE WS-REG-SALIDA-L.                                  
                                                                        
           MOVE   WS-GUION             TO WS-SAL-GUION-MM               
                                          WS-SAL-GUION-DD.              
           MOVE   20                   TO WS-SAL-SIGLO.                 
           MOVE   WS-FECHA-AA          TO WS-SAL-AA.                    
           MOVE   WS-FECHA-MM          TO WS-SAL-MM.                    
           MOVE   WS-FECHA-DD          TO WS-SAL-DD.                    
           MOVE   WS-TOTAL             TO WS-SAL-TITULO.                
           MOVE   WS-CANT-CTAS-BCO     TO WS-SAL-TOTAL.                 
                                                                        
           WRITE REG-SALIDA FROM WS-REG-SALIDA-L                        
              IF WS-SAL-CODE IS NOT EQUAL '00'                          
                DISPLAY '* ERROR EN WRITE LAST SALIDA  = '              
                                            WS-SAL-CODE                 
                MOVE 9999 TO RETURN-CODE                                
             END-IF.                                                    
      *MUESTRA EL TOTAL GENERAL A SALIDA POR CONSOLA                    
           DISPLAY '************************** '                        
           DISPLAY 'TOTAL DE CUENTAS ACTIVAS:  ' WS-SAL-TOTAL.          
           MOVE WS-CANT-LEIDAS        TO WS-EDIT-LEIDAS.                
           DISPLAY "CANTIDAD CUENTAS LEIDAS :  "  WS-EDIT-LEIDAS.       
                                                                        
       F-2100-ULTIMO-REGISTRO. EXIT.                                    
                                                                        
       2200-SEGUIR-PROCESO.                                             
                                                                        
           IF WS-NRO-SUCURSAL EQUAL WS-SUCURSAL-ANT                     
              NEXT SENTENCE                                             
           ELSE                                                         
                 PERFORM 2230-CORTE-SUCURSAL THRU                       
                       F-2230-CORTE-SUCURSAL                            
           END-IF.                                                      
                                                                        
       F-2200-SEGUIR-PROCESO. EXIT.                                     
                                                                        
      **************************************                            
      *                                    *                            
      *  PARRAFO QUE CONTROLA EL CORTE     *                            
      *  DE SUCURSAL                       *                            
      *                                    *                            
      **************************************                            
       2230-CORTE-SUCURSAL.                                             
           MOVE SPACES             TO     WS-REG-SALIDA                 
                                                                        
           MOVE WS-SUCURSAL-ANT    TO     WS-SAL-SUCURSAL               
           MOVE WS-NOMBRE-SUC-ANT  TO     WS-SAL-NOMBRE-SUC             
           MOVE WS-CANT-CTAS-SUC   TO     WS-SAL-CANT-CUENTAS           
                                                                        
      ****************************************************              
      *      MUESTRO TOTALES GRABADOS EN SALIDA          *              
      ****************************************************              
           DISPLAY "SUCURSAL:  " WS-SAL-SUCURSAL                        
           DISPLAY "NOMBRE:    " WS-SAL-NOMBRE-SUC                      
           DISPLAY "CANTIDAD CUENTAS ACTIVAS:  "  WS-SAL-CANT-CUENTAS   
      *                                                                 
      ****************************************************              
      * GUARDO LA SUCURSAL PARA PR�XIMO CORTE SUCURSAL                  
      ****************************************************              
                                                                        
           MOVE WS-NRO-SUCURSAL   TO       WS-SUCURSAL-ANT              
           MOVE WS-NOMBRE-SUC     TO       WS-NOMBRE-SUC-ANT            
           ADD  WS-CANT-CTAS-SUC  TO       WS-CANT-CTAS-BCO             
           MOVE ZEROS             TO       WS-CANT-CTAS-SUC             
                                                                        
           WRITE REG-SALIDA FROM WS-REG-SALIDA                          
           IF WS-SAL-CODE IS NOT EQUAL '00'                             
                DISPLAY '* ERROR EN WRITE SALIDA  = '                   
                                            WS-SAL-CODE                 
                MOVE 9999 TO RETURN-CODE                                
                SET WS-FIN-LECTURA TO TRUE                              
           END-IF.                                                      
                                                                        
       F-2230-CORTE-SUCURSAL. EXIT.                                     
                                                                        
      **************************************                            
      *                                    *                            
      *  CUERPO FINAL CIERRE DE FILES      *                            
      *                                    *                            
      **************************************                            
       9999-FINAL.                                                      
                                                                        
           CLOSE ENTRADA                                                
              IF WS-ENT-CODE IS NOT EQUAL '00'                          
                DISPLAY '* ERROR EN CLOSE ENTRADA = '                   
                                            WS-ENT-CODE                 
                MOVE 9999 TO RETURN-CODE                                
                SET WS-FIN-LECTURA TO TRUE                              
             END-IF.                                                    
                                                                        
           CLOSE  SALIDA                                                
              IF WS-SAL-CODE IS NOT EQUAL '00'                          
                DISPLAY '* ERROR EN CLOSE SALIDA   ='                   
                                            WS-SAL-CODE                 
                MOVE 9999 TO RETURN-CODE                                
                SET WS-FIN-LECTURA TO TRUE                              
           END-IF.                                                      
                                                                        
       F-9999-FINAL.                                                    
           EXIT.                                                        
      *                                                                 
