       IDENTIFICATION DIVISION.
       PROGRAM-ID. PGMM1C7C.
      *
      *****************************************************************
      *                                                               *
      * CONSULTA DE CLIENTES POR TIPO Y NUMERO DE DOCUMENTO          *
      *                                                               *
      *****************************************************************
      *
       DATA DIVISION.
       FILE SECTION.
       WORKING-STORAGE SECTION.

       01  CT-ERR-01              PIC X(72) VALUE
           'ERROR1 - INGRESE LOS DATOS Y PRESIONE ENTER'.
       01  CT-ERR-02              PIC X(72) VALUE
           'ERROR2 - DATOS INGRESADOS INCORRECTOS'.
       01  CT-ERR-03              PIC X(72) VALUE
           'ERROR3 - TIPO Y NRO DOCUMENTO INEXISTENTES'.
       01  CT-MNS-EXIT            PIC X(72) VALUE
           'FIN TRANSACCION BC7C'.

       01 WS-VARIABLES.
          03 WS-MAP               PIC X(07)       VALUE 'MAP1C7C'.
          03 WS-MAPSET            PIC X(07)       VALUE 'MAP1C7C'.
          03 WS-LONG              PIC S9(04) COMP.
          03 WS-ABSTIME           PIC S9(16) COMP VALUE +0.
          03 WS-FECHA             PIC X(10)       VALUE SPACES.
          03 WS-SEP-DATE          PIC X           VALUE '/'.
          03 WS-HORA              PIC X(08)       VALUE SPACES.
          03 WS-SEP-HOUR          PIC X           VALUE ':'.
          03 WS-RESP              PIC S9(04) COMP.
          03 WS-ERR               PIC X(15).

       01 WS-COMMAREA.
          03 WS-USER-DATA.
             05 WS-USER-TIPDOC    PIC X(02).
             05 WS-USER-NUMDOC    PIC 9(11).
          03 WS-TIP-DOC           PIC X(02).
             88 WS-TIP-DOC-BOOLEAN                   VALUE 'DU'
                                                           'PA'
                                                           'PE'.
          03 FILLER               PIC X(5).

       01 WS-VSAM-RESP             PIC S9(04) COMP.
       01 WS-CLIENTE-ENCONTRADO    PIC X VALUE 'N'.
          88 CLIENTE-ENCONTRADO                    VALUE 'S'.
          88 CLIENTE-NO-ENCONTRADO                 VALUE 'N'.

       COPY CPPERSON.
       COPY MAP1C7C.

       COPY DFHBMSCA.
       COPY DFHAID.

       LINKAGE SECTION.
       01 DFHCOMMAREA PIC X(20).

       PROCEDURE DIVISION.
       MAIN-PROGRAM.
           MOVE DFHCOMMAREA TO WS-COMMAREA.
           PERFORM 1000-I-INICIO THRU 1000-F-INICIO.

           PERFORM 2000-I-PROCESO

           PERFORM 9999-I-FINAL.

       1000-I-INICIO.

           MOVE LOW-VALUES TO MAP1C7CI.

      *    IF EIBCALEN = 0
              PERFORM 1500-I-RECEIVE-MAP THRU 1500-F-RECEIVE-MAP
      *    END-IF

           .
       1000-F-INICIO. EXIT.

       1500-I-RECEIVE-MAP.

           EXEC CICS
                RECEIVE MAP (WS-MAP)
                        MAPSET (WS-MAPSET)
                        INTO (MAP1C7CI)
                        RESP(WS-RESP)
           END-EXEC.

       1500-F-RECEIVE-MAP. EXIT.

       2000-I-PROCESO.
           EVALUATE WS-RESP
             WHEN DFHRESP (NORMAL)
               MOVE LENGTH OF MAP1C7CO TO WS-LONG
               PERFORM 2500-I-PULSAR-TECLA THRU 2500-F-PULSAR-TECLA

             WHEN DFHRESP (MAPFAIL)
               MOVE LENGTH OF MAP1C7CO TO WS-LONG
               MOVE LOW-VALUES TO MAP1C7CO
               MOVE CT-ERR-01 TO MSGO

             WHEN OTHER
               INITIALIZE MAP1C7CO
               MOVE CT-ERR-01 TO MSGO

           END-EVALUATE.

           PERFORM 9500-I-SENDMAP THRU 9500-F-SENDMAP.


       2500-I-PULSAR-TECLA.
             EVALUATE EIBAID

             WHEN DFHENTER
                 PERFORM 3000-I-ENTER THRU 3000-F-ENTER

             WHEN DFHPF3
                 PERFORM 4000-I-PF3 THRU 4000-F-PF3

             WHEN DFHPF4
                 PERFORM 4500-I-PF4 THRU 4500-F-PF4

             WHEN DFHPF12
                 PERFORM 5500-I-PF12 THRU 5500-F-PF12

             WHEN OTHER
                 MOVE 'TECLA INVALIDA' TO MSGO

             END-EVALUATE.

       2500-F-PULSAR-TECLA. EXIT.

       3000-I-ENTER.
      *    VALIDAR Y BUSCAR CLIENTE
           PERFORM 3100-I-VALIDAR-DATOS THRU 3100-F-VALIDAR-DATOS
           IF WS-RESP = DFHRESP(NORMAL)
              PERFORM 3200-I-BUSCAR-CLIENTE THRU 3200-F-BUSCAR-CLIENTE
           END-IF.

       3000-F-ENTER. EXIT.

       3100-I-VALIDAR-DATOS.
      *    VALIDAR TIPO DE DOCUMENTO
           MOVE TIPDOCI TO WS-TIP-DOC.
           IF NOT WS-TIP-DOC-BOOLEAN
              MOVE CT-ERR-02 TO MSGO
              MOVE 1 TO WS-RESP
           ELSE
              IF NUMDOCI NOT NUMERIC OR NUMDOCI = ZEROS
                 MOVE CT-ERR-02 TO MSGO
                 MOVE 1 TO WS-RESP
              ELSE
                 MOVE DFHRESP(NORMAL) TO WS-RESP
              END-IF
           END-IF.

       3100-F-VALIDAR-DATOS. EXIT.

       3200-I-BUSCAR-CLIENTE.
      *    BUSCAR EN ARCHIVO VSAM
           MOVE TIPDOCI TO PER-TIP-DOC.
           MOVE NUMDOCI TO PER-NRO-DOC.

           EXEC CICS
                READ FILE('PERSONA')
                     INTO(REG-PERSONA)
                     RIDFLD(PER-CLAVE)
                     RESP(WS-VSAM-RESP)
           END-EXEC.

           EVALUATE WS-VSAM-RESP
             WHEN DFHRESP(NORMAL)
               SET CLIENTE-ENCONTRADO TO TRUE
               PERFORM 3300-I-MOSTRAR-DATOS THRU 3300-F-MOSTRAR-DATOS
             WHEN DFHRESP(NOTFND)
               MOVE CT-ERR-03 TO MSGO
               PERFORM 3400-I-LIMPIAR-DATOS THRU 3400-F-LIMPIAR-DATOS
             WHEN OTHER
               MOVE CT-ERR-03 TO MSGO
               PERFORM 3400-I-LIMPIAR-DATOS THRU 3400-F-LIMPIAR-DATOS
           END-EVALUATE.

       3200-F-BUSCAR-CLIENTE. EXIT.

       3300-I-MOSTRAR-DATOS.
      *    MOSTRAR DATOS DEL CLIENTE EN EL MAPA
           MOVE PER-CLI-NRO    TO CLINROO.
           MOVE PER-NOMAPE     TO NOMAPEO.
           MOVE PER-DIRECCION  TO DIRECCO.
           MOVE PER-EMAIL      TO EMAILO.
           MOVE PER-TELEFONO   TO TELEFO.
           MOVE SPACES         TO MSGO.

       3300-F-MOSTRAR-DATOS. EXIT.

       3400-I-LIMPIAR-DATOS.
      *    LIMPIAR CAMPOS DE SALIDA
           MOVE ZERO TO CLINROO.
           MOVE SPACES TO NOMAPEO.
           MOVE SPACES TO DIRECCO.
           MOVE SPACES TO EMAILO.
           MOVE SPACES TO TELEFO.

       3400-F-LIMPIAR-DATOS. EXIT.
       4000-I-PF3.
      *    LIMPIAR PANTALLA
           MOVE LOW-VALUES TO MAP1C7CI.
           MOVE LOW-VALUES TO MAP1C7CO.
           MOVE SPACES TO MSGO.

       4000-F-PF3. EXIT.

       4500-I-PF4.
      *    SIGUIENTE (NO IMPLEMENTADO EN ESTA VERSION)
           MOVE 'FUNCION NO DISPONIBLE' TO MSGO.

       4500-F-PF4. EXIT.

       5500-I-PF12.
           EXEC CICS
                SEND CONTROL ERASE
           END-EXEC

           EXEC CICS
                SEND TEXT
                     FROM (CT-MNS-EXIT)
           END-EXEC

           EXEC CICS
                RETURN
           END-EXEC.
       5500-F-PF12. EXIT.

       7000-I-TIME.

           EXEC CICS ASKTIME
             ABSTIME (WS-ABSTIME)
           END-EXEC.

           EXEC CICS FORMATTIME
             ABSTIME (WS-ABSTIME)
             DDMMYYYY (WS-FECHA) DATESEP(WS-SEP-DATE)
             TIME (WS-HORA) TIMESEP(WS-SEP-HOUR)
           END-EXEC.

           MOVE WS-FECHA TO FECHAO.

       7000-F-TIME. EXIT.

       9500-I-SENDMAP.

           PERFORM 7000-I-TIME THRU 7000-F-TIME
           EXEC CICS
                SEND MAP    (WS-MAP)
                     MAPSET (WS-MAPSET)
                     FROM   (MAP1C7CO)
                     LENGTH (WS-LONG)
                     ERASE
                     FREEKB
           END-EXEC.
           EXEC CICS
                RETURN
                TRANSID  ('BC7C')
           END-EXEC.
       9500-F-SENDMAP. EXIT.

       9999-I-FINAL.

           EXEC CICS
             RETURN
           END-EXEC.
       9999-F-FINAL. EXIT.

